# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>

pkgname=passim
pkgdesc="A local caching server. Named after the Latin word for “here, there and everywhere”."
license=(LGPL)

arch=(x86_64)
pkgver=0.1.5+82+bfc3a52
pkgrel=2

url="https://github.com/hughsie/passim" 

depends=(
  avahi
  glib2 libgio-2.0.so
  gnutls
  hicolor-icon-theme
  libsoup3 libsoup-3.0.so
)

makedepends=(
  cmake
  gobject-introspection
  meson
  systemd
)

b2sums=(SKIP)
source=(git+https://github.com/hughsie/passim.git)

provides=(libpassim.so)
backup=(
  etc/passim.conf
)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
