# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice
pkgdesc="D-Bus interface for user account query and manipulation"
license=(GPL3)

arch=(x86_64)
pkgver=23.39.18+747+42aa71a
pkgrel=10

url="https://gitlab.freedesktop.org/accountsservice/accountsservice"

depends=(
  glib2
  libxcrypt
  polkit
  shadow
  systemd
)

makedepends=(
  git
  gobject-introspection
  meson
  vala
  xmlto
)

b2sums=(SKIP)
source=(git+https://gitlab.freedesktop.org/accountsservice/accountsservice.git)

provides=(libaccountsservice.so)

pkgver() {
	cd $pkgname

	local _version=$( ./generate-version.sh)
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D admin_group=wheel
	)
	
	arch-meson $pkgname build "${meson_options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
