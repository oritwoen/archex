# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice
pkgdesc="D-Bus interface for user account query and manipulation"
license=(GPL3)

arch=(x86_64)
pkgver=23.39.18+747+42aa71a
pkgrel=26

url="https://gitlab.freedesktop.org/accountsservice/accountsservice"

depends=(
	glib2
	polkit
	shadow
	systemd
	libxcrypt
)

makedepends=(
	vala
	gobject-introspection
)

b2sums=(SKIP)
source=(git+https://gitlab.freedesktop.org/accountsservice/accountsservice.git#branch=main)

provides=(libaccountsservice.so)

pkgver() {
  cd $pkgname

	local _version=$( ./generate-version.sh)
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

  echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D systemdsystemunitdir=''
		-D gdmconffile=/etc/gdm/custom.conf
		-D lightdmconffile=/etc/lightdm/lightdm.conf
		-D admin_group=wheel
		-D extra_admin_groups=[]
		-D minimum_uid=1000
		-D elogind=false
		-D libxcrypt=true
		-D introspection=true
		-D vapi=true
		-D docbook=false
		-D gtk_doc=false
	)
	
	arch-meson $pkgname build "${options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
