# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice
pkgdesc="D-Bus interface for user account query and manipulation"
license=(GPL-3.0-or-later)

arch=(x86_64)
pkgver=24.17.41+770+001fb13
pkgrel=1

url="https://gitlab.freedesktop.org/accountsservice/accountsservice"

depends=(
	glib2
	glibc
	polkit
	shadow
	systemd
	gcc-libs
	libxcrypt
)

makedepends=(
	vala
	python-packaging
	gobject-introspection
)

b2sums=(SKIP SKIP)
source=(
	accounts-daemon-restart.hook
	git+https://gitlab.freedesktop.org/accountsservice/accountsservice.git#branch=main
)

provides=(libaccountsservice.so)

pkgver() {
	cd $pkgname

	local _version=$( ./generate-version.sh)
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D admin_group=wheel
	)
	
	arch-meson $pkgname build "${options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
	
	install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
	install -d "$pkgdir/usr/share/accountsservice/interfaces"
}
