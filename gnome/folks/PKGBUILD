# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.15.6+3008+bc03cbab
pkgrel=8
pkgdesc="Library to aggregates people into metacontacts"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/Folks"
license=(LGPL)
depends=(libgee libxml2 evolution-data-server)
makedepends=(gobject-introspection gtk-doc vala git meson python-dbusmock)
provides=(libfolks.so libfolks-{dummy,eds}.so)
source=("git+https://gitlab.gnome.org/GNOME/folks.git")
sha256sums=('SKIP')

pkgver () {
    cd ${pkgname}

    local _version=$(cat meson.build | grep "version:" |  head -n 1 | cut -d "'" -f2 | cut -d "'" -f1)

    echo ${_version}+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
}

build() {
  local meson_options=(
    -D b_lto=false
    -D tests=false
  )

  arch-meson folks build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
