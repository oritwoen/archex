
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=3.47.2+r4+g5e7c00392
pkgrel=4
pkgdesc="Centralized access to appointments and contacts"
url="https://wiki.gnome.org/Apps/Evolution"
arch=(x86_64)
depends=(
  gcr-4
  gnome-online-accounts
  krb5
  libcanberra
  libgweather-4
  libical
  libphonenumber
  nss
  sqlite
  webkit2gtk-4.1
  webkitgtk-6.0
)
makedepends=(
  boost
  cmake
  git
  gobject-introspection
  gperf
  gtk-doc
  ninja
  vala
)

license=(GPL)
source=("git+https://gitlab.gnome.org/GNOME/evolution-data-server.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DLIBEXEC_INSTALL_DIR=/usr/lib
    -DSYSCONF_INSTALL_DIR=/etc
    -DENABLE_GTK_DOC=ON
    -DENABLE_INTROSPECTION=ON
    -DENABLE_VALA_BINDINGS=ON
    -DWITH_GWEATHER4=ON
    -DWITH_LIBDB=OFF
    -DWITH_PHONENUMBER=ON
  )

  cmake -S $pkgname -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  cd build
  ctest --output-on-failure --stop-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
