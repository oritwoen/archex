# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-maps
pkgver=44.rc+3548+a0e5b022
pkgrel=3
pkgdesc="A simple GNOME 3 maps application"
url="https://wiki.gnome.org/Apps/Maps"
arch=(x86_64)
license=(GPL)
depends=(geoclue geocode-glib-2 libadwaita libshumate appstream-glib gjs libgweather-4 librest)
makedepends=(gobject-introspection git meson yelp-tools)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-maps.git")
sha256sums=('SKIP')

pkgver () {
    cd ${pkgname}

    local _version=$(cat meson.build | grep "version:" |  head -n 1 | cut -d "'" -f2 | cut -d "'" -f1)

    echo ${_version}+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
}

build() {
  arch-meson $pkgname build

  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
