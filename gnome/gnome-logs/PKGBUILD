# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-logs
pkgver=43.1+1280+e6de502
pkgrel=6
pkgdesc="A log viewer for the systemd journal"
url="https://wiki.gnome.org/Apps/Logs"
arch=(x86_64)
license=(GPL)
depends=(systemd gtk3 gsettings-desktop-schemas)
makedepends=(appstream-glib git meson yelp-tools)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-logs.git")
sha256sums=('SKIP')

pkgver() {
    cd $pkgname
    local _version=$(cat meson.build | grep "version :" | head -n 1 | cut -d "'" -f6)
    echo ${_version}+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
}

build() {
    arch-meson $pkgname build
    meson compile -C build
}

package() {
    DESTDIR="$pkgdir" meson install -C build
}
