# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-session
pkgver=43.0+6652+f3593725
pkgrel=11
pkgdesc="The GNOME Session Handler"
url="https://gitlab.gnome.org/GNOME/gnome-session"
arch=(x86_64)
license=(GPL)
depends=(systemd dconf gsettings-desktop-schemas gtk3 gnome-desktop
         json-glib libgl libsm libxtst systemd)
makedepends=(mesa gtk-doc xtrans git python xmlto docbook-xsl meson)
groups=(gnome-core)
source=("git+https://gitlab.gnome.org/GNOME/gnome-session.git#branch=main")
sha256sums=('SKIP')

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build \
        -D docbook=false \
        -D man=false

    meson compile -C build
}

package() {
    DESTDIR="$pkgdir" meson install -C build
}
