# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Michael Kanis <mkanis_at_gmx_dot_de>

pkgname=mutter
pkgver=44.beta+29984+4a97da367
pkgrel=1
pkgdesc="Window manager and compositor for GNOME"
url="https://gitlab.gnome.org/GNOME/mutter"
arch=(x86_64)
license=(GPL)
depends=(
  colord
  dconf
  gnome-desktop
  gnome-settings-daemon
  graphene
  gsettings-desktop-schemas
  lcms2
  libcanberra
  libgudev
  libinput
  libsm
  libsysprof-capture
  libxkbcommon-x11
  libxkbfile
  pipewire
  startup-notification
  xorg-xwayland
)
makedepends=(
  egl-wayland
  gi-docgen
  git
  gobject-introspection
  meson
  sysprof
  wayland-protocols
  xorg-server
)

source=(
  "git+https://gitlab.gnome.org/GNOME/mutter.git#branch=main"
)
b2sums=('SKIP')

groups=(gnome-core)

#LEGACY - only for correct install (gnome-shell conflict)
#WARNING: gnome-shell should be build right after this
provides=(libmutter-11.so=0-64)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson mutter build \
        -D egl_device=true \
        -D wayland_eglstream=true \
        -D cogl_tests=false \
        -D clutter_tests=false \
        -D core_tests=false \
        -D native_tests=false \
        -D tests=false \
        -D profiler=false \
        -D installed_tests=false
  
    meson compile -C build
}


package() {
    meson install -C build --destdir "$pkgdir"
}
