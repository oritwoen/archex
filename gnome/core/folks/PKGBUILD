# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.15.6+3009+ab2719f1
pkgrel=17
pkgdesc="Library to aggregates people into metacontacts"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/Folks"
license=(LGPL)
depends=(libgee libxml2 evolution-data-server)
makedepends=(gobject-introspection gtk-doc vala git meson python-dbusmock)
provides=(libfolks.so libfolks-{dummy,eds}.so)
source=("git+https://gitlab.gnome.org/GNOME/folks.git")
sha256sums=('SKIP')

groups=(gnome-core)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    local meson_options=(
      -D tests=false
    )

    arch-meson folks build "${meson_options[@]}"
    
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
