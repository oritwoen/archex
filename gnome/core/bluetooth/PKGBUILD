# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>

pkgname=gnome-bluetooth
pkgdesc="GNOME Bluetooth Subsystem"
license=(GPL LGPL)

arch=(x86_64)
pkgver=42.5+3647+01706fac
pkgrel=1

url=https://wiki.gnome.org/Projects/GnomeBluetooth

depends=(gtk4 libadwaita libnotify bluez gsound systemd pulseaudio-bluetooth upower)
makedepends=(gobject-introspection git meson)

groups=(gnome/core)
source=(git+https://gitlab.gnome.org/gnome/gnome-bluetooth.git)
b2sums=(SKIP)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build
    
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
