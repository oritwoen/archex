
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=3.47.2+r18+g2293dc1dd
pkgrel=3
pkgdesc="Centralized access to appointments and contacts"
url="https://wiki.gnome.org/Apps/Evolution"
arch=(x86_64)
depends=(
  gcr-4
  gnome-online-accounts
  krb5
  libcanberra
  libgweather-4
  libical
  libphonenumber
  nss
  sqlite
  webkitgtk-6.0
)
makedepends=(
  boost
  cmake
  git
  gobject-introspection
  gperf
  gtk-doc
  ninja
  vala
)

license=(GPL)
source=("git+https://gitlab.gnome.org/GNOME/evolution-data-server.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DLIBEXEC_INSTALL_DIR=/usr/lib
    -DSYSCONF_INSTALL_DIR=/etc
    -DENABLE_GTK_DOC=OFF
    -DENABLE_INTROSPECTION=ON
    -DENABLE_VALA_BINDINGS=ON
    -DWITH_LIBDB=OFF
    -DWITH_PHONENUMBER=ON
    -DENABLE_EXAMPLES=OFF
  )
  
  cmake -S $pkgname -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
