# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=adwaita-icon-theme
pkgver=44.beta+r3+gea00e436a+3828+ea00e436a
pkgrel=2
pkgdesc="GNOME standard icons"
url="https://gitlab.gnome.org/GNOME/adwaita-icon-theme"
arch=(any)
license=(LGPL3 CCPL:by-sa)
depends=(hicolor-icon-theme gtk-update-icon-cache librsvg)
makedepends=(git gtk3)
source=("git+https://gitlab.gnome.org/GNOME/adwaita-icon-theme.git#branch=master")
sha256sums=('SKIP')

groups=(gnome-core)

provides=(adwaita-cursors)
replaces=(adwaita-cursors)
conflicts=(adwaita-cursors)

pkgver() {
  cd $pkgname
  
  echo $(git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g')+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname
  autoreconf -fvi
}
  
build() {
  cd $pkgname
  ./configure --prefix=/usr
  
  make
}

package() {
  make -C $pkgname DESTDIR="$pkgdir" install
}
