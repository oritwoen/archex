# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

# Name change due to API change of former webkit2gtk-5.0 to webkitgtk-6.0. This also reflects the library names.
# Other Projects
# https://gitlab.gnome.org/GNOME/gnome-build-meta/-/merge_requests/1864
# https://src.fedoraproject.org/rpms/webkitgtk/c/f1d95b90e57fec87a68637fb6270dd7ab4dbd50f?branch=rawhide

pkgname=webkitgtk-6.0
pkgver=2.39.90
pkgrel=13
pkgdesc="Web content engine for GTK"
url="https://webkitgtk.org"
arch=(x86_64)
license=(custom)
depends=(
  at-spi2-core
  atk
  bubblewrap
  cairo
  enchant
  fontconfig
  freetype2
  glib2
  gst-plugins-base-libs
  gstreamer
  gtk4
  harfbuzz
  harfbuzz-icu
  hyphen
  icu
  libavif
  libegl
  libgcrypt
  libgl
  libgles
  libice
  libjpeg
  libjxl
  libmanette
  libnotify
  libpng
  libseccomp
  libsecret
  libsoup3
  libsystemd
  libtasn1
  libwebp
  libwpe
  libx11
  libxcomposite
  libxext
  libxml2
  libxslt
  libxt
  openjpeg2
  sqlite
  wayland
  woff2
  wpebackend-fdo
  xdg-dbus-proxy
  zlib
)
makedepends=(
  cmake
  gi-docgen
  gobject-introspection
  gperf
  gst-plugins-bad
  ninja
  python
  ruby
  systemd
  unifdef
  wayland-protocols
)

# !lto: https://bugs.webkit.org/show_bug.cgi?id=249368
options=(!debug !lto)
source=($url/releases/webkitgtk-$pkgver.tar.xz)
sha256sums=('SKIP')
prepare() {
  cd webkitgtk-$pkgver
}

build() {
  cmake -S webkitgtk-$pkgver -B build -G Ninja \
    -DPORT=GTK \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_SKIP_RPATH=ON \
    -DUSE_AVIF=ON \
    -DUSE_GTK4=ON \
    -DUSE_JPEGXL=ON \
    -DENABLE_DOCUMENTATION=OFF \
    -DENABLE_MINIBROWSER=ON

  cmake --build build
}

package() {
  depends+=(libwpe-1.0.so libWPEBackend-fdo-1.0.so)
  provides+=(libjavascriptcoregtk-6.0.so libwebkitgtk-6.0.so)

  #Legacy option to make other packages work.
  provides+=(webkit2gtk-5.0)

  replaces=(webkit2gtk-5.0)
  conflicts=(webkit2gtk-5.0)
  optdepends=('geoclue: Geolocation support'
              'gst-plugins-good: media decoding'
              'gst-plugins-bad: media decoding'
              'gst-libav: nonfree media decoding')

  DESTDIR="$pkgdir" cmake --install build

  cd webkitgtk-$pkgver
  find Source -name 'COPYING*' -or -name 'LICENSE*' -print0 | sort -z |
    while IFS= read -d $'\0' -r _f; do
      echo "### $_f ###"
      cat "$_f"
      echo
    done |
    install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
