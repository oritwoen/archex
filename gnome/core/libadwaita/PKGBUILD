# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libadwaita
pkgver=1.4.alpha+3410+527d9883
pkgrel=1
pkgdesc="Building blocks for modern adaptive GNOME applications"
url="https://gnome.pages.gitlab.gnome.org/libadwaita/"
arch=(x86_64)
license=(LGPL)
#depends=(gtk4)
makedepends=(git meson sassc gobject-introspection vala)
source=("git+https://gitlab.gnome.org/GNOME/libadwaita.git#branch=main")
sha256sums=('SKIP')

groups=(gnome-core)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build \
        -D tests=false \
        -D examples=false
    
    meson compile -C build
}


package() {
    meson install -C build --destdir "$pkgdir"
}
