# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-color-manager
pkgver=3.36.1+2903+a3f5d8b4
pkgrel=8
pkgdesc="GNOME Color Profile Tools"
url="https://gitlab.gnome.org/GNOME/gnome-color-manager"
arch=(x86_64)
license=(GPL2)
depends=(shared-color-targets colord lcms2)
makedepends=(yelp-tools git appstream-glib meson perl-sgmls)
checkdepends=(xorg-server-xvfb)
groups=(gnome-core)
source=("git+https://gitlab.gnome.org/GNOME/gnome-color-manager.git#branch=master")
sha256sums=('SKIP')

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
	arch-meson $pkgname build \
		-D tests=false

    meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
