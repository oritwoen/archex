# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgver=44.rc+4196+a90febff
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(x86_64)
license=(GPL)
depends=(evolution-data-server gsettings-desktop-schemas gnome-control-center libdazzle)
makedepends=(python appstream-glib git meson)
optdepends=('evolution: ICS file import')
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-calendar.git")
sha256sums=('SKIP')

pkgver() {
    cd ${pkgname}

    local _version=$(cat meson.build | grep "version:" | sed -n 1p | cut -d "'" -f2 | cut -d "'" -f1)

    echo ${_version}+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
}

build() {
    arch-meson $pkgname build

    meson compile -C build
}

package() {
    DESTDIR="$pkgdir" meson install -C build
}
