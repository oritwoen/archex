# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgdesc="A GNOME web browser based on the WebKit rendering engine"

pkgver=44.beta+r25+gacf20149f
pkgrel=2
url="https://wiki.gnome.org/Apps/Web"
arch=(x86_64)
license=(GPL)
depends=(webkit2gtk-4.1 gcr icu libdazzle libhandy libarchive libportal-gtk3
         libsoup3)
makedepends=(docbook-xml startup-notification lsb-release gobject-introspection
             yelp-tools git meson)
groups=(gnome-core-apps)
source=("git+https://gitlab.gnome.org/GNOME/epiphany.git#branch=master")
sha256sums=('SKIP')

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build

    meson compile -C build
}

package () {
    meson install -C build --destdir "$pkgdir"
}
