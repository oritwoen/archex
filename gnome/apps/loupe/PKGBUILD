# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=loupe
pkgver=44.alpha+208+30136f9
pkgrel=1
pkgdesc="A simple image viewer for GNOME"
url="https://gitlab.gnome.org/Incubator/loupe"
arch=(any)
license=(GPL)
depends=(
  gtk4
  libadwaita
  libgweather-4
)
makedepends=(
  git
  itstool
  meson
  rust
)

source=("git+https://gitlab.gnome.org/Incubator/loupe.git")
sha256sums=('SKIP')

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}


build() {
    arch-meson $pkgname build
    
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
