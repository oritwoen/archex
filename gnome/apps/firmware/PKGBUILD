# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=gnome-firmware
pkgver=+266+e51a899
pkgrel=3
pkgdesc="Manage firmware on devices supported by fwupd"
arch=(x86_64)
url="https://gitlab.gnome.org/world/gnome-firmware"
license=(GPL2)
depends=(gtk4 glib2 fwupd libadwaita libxmlb systemd)
makedepends=(meson help2man)
source=(git+https://gitlab.gnome.org/world/gnome-firmware)
sha256sums=('SKIP')

pkgver() {
    cd ${pkgname}

    local _version=$(grep -m 1 "version:" meson.build | cut -d "'" -f2)

    echo ${_version}+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
}

build() {
  arch-meson ${pkgname} build -D man=false

  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
