# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Igor Dyatlov <dyatlov.igor@gmail.com>

pkgname=gnome-console
pkgver=44.rc+664+4452533
pkgrel=1
pkgdesc="A simple user-friendly terminal emulator for the GNOME desktop"
url="https://gitlab.gnome.org/GNOME/console"
arch=(x86_64)
license=(GPL3)
depends=(libgtop libadwaita vte4)
makedepends=(appstream-glib git meson)
groups=(gnome-apps)
source=("$pkgname::git+https://gitlab.gnome.org/GNOME/console.git#branch=main")
sha256sums=('SKIP')

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build
  
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
