# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-weather
pkgver=44.rc+1418+fb32b6c
pkgrel=3
pkgdesc="Access current weather conditions and forecasts"
url="https://wiki.gnome.org/Apps/Weather"
arch=(any)
license=(GPL)
depends=(gjs libgweather-4 geoclue libadwaita)
makedepends=(gobject-introspection appstream-glib git meson)
groups=(gnome-apps)
source=("git+https://gitlab.gnome.org/GNOME/gnome-weather.git#branch=main")
sha256sums=('SKIP')

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build
  
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}

