# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-screenshot
pkgver=41.0+1421+b4a55ef
pkgrel=10
pkgdesc="Take pictures of your screen"
url="https://gitlab.gnome.org/GNOME/gnome-screenshot"
arch=(x86_64)
license=(GPL2)
depends=(gtk3 libcanberra)
makedepends=(git meson appstream-glib)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-screenshot.git")
sha256sums=('SKIP')

pkgver() {
    cd ${pkgname}

    local _version=$(cat meson.build | grep "version:" | sed -n 1p | cut -d "'" -f2 | cut -d "'" -f1)

    echo ${_version}+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
}

build() {
    arch-meson $pkgname build

    meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
