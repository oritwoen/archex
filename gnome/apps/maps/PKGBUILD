# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-maps
pkgver=44.0+3575+1669c7e5
pkgrel=1
pkgdesc="A simple GNOME 3 maps application"
url="https://wiki.gnome.org/Apps/Maps"
arch=(x86_64)
license=(GPL)
depends=(geoclue geocode-glib-2 libadwaita libshumate appstream-glib gjs libgweather-4 librest)
makedepends=(gobject-introspection git meson yelp-tools)
groups=(gnome-apps)
source=("git+https://gitlab.gnome.org/GNOME/gnome-maps.git#branch=main")
sha256sums=('SKIP')

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build
  
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
