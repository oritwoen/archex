# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=nautilus
pkgver=44.alpha.1+23828+c83a73fe0
pkgrel=1
pkgdesc="Default file manager for GNOME"
url="https://wiki.gnome.org/Apps/Files"
arch=(x86_64)
license=(GPL)
depends=(libgexiv2 gnome-desktop gvfs dconf tracker nautilus-sendto gnome-autoar
         gst-plugins-base-libs tracker-miners gtk3)
makedepends=(gobject-introspection git gtk-doc meson appstream-glib)
source=("git+https://gitlab.gnome.org/GNOME/nautilus.git")
sha256sums=('SKIP')

groups=(gnome)
replaces=(libnautilus-extension)
conflicts=(libnautilus-extension)
provides=(libnautilus-extension.so)

pkgver () {
    cd ${pkgname}

    local _version=$(cat meson.build | grep "version:" |  head -n 1 | cut -d "'" -f2 | cut -d "'" -f1)

    echo ${_version}+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
}

build() {
  arch-meson $pkgname build \
    -D tests=none

  meson compile -C build
}

package () {
  DESTDIR="$pkgdir" meson install -C build
}
