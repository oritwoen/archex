# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Igor Dyatlov <dyatlov.igor@gmail.com>

pkgname=gnome-console
pkgdesc="A simple user-friendly terminal emulator for the GNOME desktop"
license=(GPL3)

arch=(x86_64)
pkgver=45.rc+755+dd0be2a
pkgrel=1

url="https://gitlab.gnome.org/GNOME/console"

depends=(vte4 libgtop libadwaita)
makedepends=(git meson appstream-glib)

b2sums=(SKIP)
groups=(gnome)
source=($pkgname::git+https://gitlab.gnome.org/GNOME/console.git#branch=main)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build
  
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
