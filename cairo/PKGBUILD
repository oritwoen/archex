# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

pkgname=cairo
pkgdesc="2D graphics library with support for multiple output devices"

arch=(x86_64)
pkgver=1.18.1+12832+f9358f26b
pkgrel=3

url=https://cairographics.org/
license=(LGPL MPL)

depends=(
	lzo
	zlib
	glib2
	pixman
	libpng
	libx11
	libxcb
	libxext
	freetype2
	libxrender
	fontconfig
)

makedepends=(
	valgrind
)

b2sums=(SKIP)
source=(git+https://gitlab.freedesktop.org/cairo/cairo.git#branch=master)
provides=(
	libcairo-gobject.so
	libcairo-script-interpreter.so
	libcairo.so
	libcairo.so=2-64
)

pkgver() {
	cd $pkgname
	
	local _version=$(python version.py)
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D dwrite=disabled
		-D fontconfig=enabled
		-D freetype=enabled
		-D png=enabled
		-D quartz=disabled
		-D tee=enabled
		-D xcb=enabled
		-D xlib=enabled
		-D xlib-xcb=enabled
		-D zlib=enabled
		-D tests=disabled
		-D gtk2-utils=disabled
		-D glib=enabled
		-D spectre=disabled
		-D symbol-lookup=disabled
		-D gtk_doc=false
	)
		
	arch-meson $pkgname build "${options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
