# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

pkgbase=cairo
pkgname=cairo
pkgver=1.17.9+12769+42c986fef
pkgrel=2
pkgdesc="2D graphics library with support for multiple output devices"
url="https://cairographics.org/"
arch=(x86_64)
license=(LGPL MPL)

depends=(
	fontconfig
	freetype2
	glib2
	libpng
	libx11
	libxcb
	libxext
	libxrender
	lzo
	pixman
	zlib
)

makedepends=(
	git
	meson
	valgrind
)

b2sums=(SKIP)
source=(git+https://gitlab.freedesktop.org/cairo/cairo.git)
provides=(
	libcairo-gobject.so
	libcairo-script-interpreter.so
	libcairo.so
	libcairo.so=2-64
)


pkgver() {
	cd $pkgname
	
    local _version=$(python version.py)
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D tests=disabled
		-D symbol-lookup=disabled
	)
		
	arch-meson cairo build "${meson_options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
