# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libsoup3
pkgver=3.4.2+3866+57403da8
pkgrel=5
pkgdesc="HTTP client/server library for GNOME"
url="https://wiki.gnome.org/Projects/libsoup"
arch=(x86_64)
license=(LGPL)
depends=(
	brotli
	glib-networking
	glib2
	krb5
	libnghttp2
	libpsl
	libsysprof-capture
	sqlite
)
makedepends=(
	git
	gobject-introspection
	meson
	python-quart
	samba
	vala
)

depends=(
	libbrotlidec.so
	libgssapi_krb5.so
	libg{lib,object,io}-2.0.so
	libpsl.so
)
optdepends=('samba: Windows Domain SSO')
provides=(libsoup-3.0.so libsoup-3.0.so=0-64)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/libsoup.git)

pkgver() {
	cd libsoup
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D docs=disabled
		-D doc_tests=false
		-D tests=false
		-D autobahn=disabled
		-D sysprof=disabled
		-D pkcs11_tests=disabled
	)
	
	arch-meson libsoup build "${meson_options[@]}"
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
