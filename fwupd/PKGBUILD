# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=fwupd
pkgdesc="Simple daemon to allow session software to update firmware"
license=(LGPL-2.1-or-later)

arch=(x86_64)
pkgver=2.0.0+10492+74aa49431
pkgrel=1

url=https://github.com/fwupd/fwupd

depends=(
	gcab
	curl
	bluez
	libqmi
	passim
	efivar
	polkit
	python
	gnutls
	libcbor
	libxmlb
	libgusb
	libjcat
	libgudev
	tpm2-tss
	flashrom
	libsmbios
	fwupd-efi
	json-glib
	libmm-glib
	libarchive
	protobuf-c
	shared-mime-info
	hicolor-icon-theme
)

makedepends=(
	vala
	pandoc
	valgrind
	gi-docgen
	noto-fonts
	gnu-efi-libs
	python-cairo
	python-pillow
	python-gobject
	noto-fonts-cjk
	bash-completion
	gobject-introspection
)

b2sums=(
	SKIP 
	SKIP
)

source=(
	fwupd.sysusers
	git+https://github.com/fwupd/fwupd.git#branch=main 
)

provides=(libfwupd.so)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D docs=disabled
		-D launchd=disabled
		-D elogind=disabled
		-D tests=false
		-D systemd_unit_user=fwupd
	)
	
	arch-meson $pkgname build "${meson_options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"

	# Add fwupd user https://bugs.archlinux.org/task/79653
	install -D -m644 fwupd.sysusers "${pkgdir}"/usr/lib/sysusers.d/fwupd.conf
}
