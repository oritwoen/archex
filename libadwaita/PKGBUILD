# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libadwaita
pkgdesc="Building blocks for modern adaptive GNOME applications"
license=(LGPL-2.1-or-later)

arch=(x86_64)
pkgver=1.6.alpha+4218+6063a352
pkgrel=1

url=https://gnome.pages.gitlab.gnome.org/libadwaita

depends=(
	gtk4
	pango
	glib2
	fribidi
	graphene
	appstream
)

makedepends=(
	vala
	sassc
	gobject-introspection
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/libadwaita.git#branch=main)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D tests=false
		-D examples=false
	)

	arch-meson $pkgname build "${options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
