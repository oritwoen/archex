# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libadwaita
pkgdesc="Building blocks for modern adaptive GNOME applications"
license=(LGPL)

arch=(x86_64)
pkgver=1.5.alpha+3904+42c04e03
pkgrel=1

url="https://gnome.pages.gitlab.gnome.org/libadwaita/"

depends=(appstream gtk4)
makedepends=(
    git
    vala
    meson
    sassc
    gobject-introspection
)

b2sums=(SKIP)
groups=(gnome-core)
source=(git+https://gitlab.gnome.org/GNOME/libadwaita.git#branch=main)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D profiling=false
        -D introspection=enabled
        -D vapi=true
        -D gtk_doc=false
        -D tests=false
        -D examples=false
	)

	arch-meson $pkgname build "${meson_options[@]}"
	meson compile -C build
}



package() {
    meson install -C build --destdir "$pkgdir"
}
