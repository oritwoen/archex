# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Flamelab <panosfilip@gmail.com>

pkgname=gnome-shell
pkgdesc="Next generation desktop shell"
license=(GPL)

arch=(x86_64)
pkgver=46.beta+18629+bd992f7dc
pkgrel=1

url=https://wiki.gnome.org/Projects/GnomeShell

depends=(
	gcr
	gjs
	gtk4
	unzip
	mutter
	upower
	accountsservice
	gsettings-desktop-schemas

	libgdm
	libibus
	libsoup3
	libsecret
	libadwaita
	libnma-gtk4
	libgweather-4
	libcanberra-pulse
	
	gnome-autoar
	gnome-session
	gnome-settings-daemon
)

makedepends=(
	sassc
	bash-completion
	gnome-control-center
	evolution-data-server
	gobject-introspection
)

b2sums=(
	SKIP
	SKIP
)

groups=(gnome)
source=(
	git+https://gitlab.gnome.org/GNOME/gnome-shell.git#branch=main
	git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git#branch=master
)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

prepare() {
	cd $pkgname

	git submodule init
	git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
	git -c protocol.file.allow=always submodule update
}

build() {
	local options=(
		-D man=false
		-D tests=false
	)

	arch-meson $pkgname build "${options[@]}"

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
