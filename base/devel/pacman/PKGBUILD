# Maintainer: Dominik Opyd <dominik.opyd@gmail.com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Morten Linderud <foxboron@archlinux.org>

pkgname=pacman
pkgdesc="A library-based package manager with dependency support"
license=('GPL')

arch=('x86_64')
pkgver=6.0.1+7214+2c45e854
pkgrel=1

url="https://www.archlinux.org/pacman/"

groups=('base-devel')
depends=(
    curl
    bash
    grep
    gawk
    gpgme
    glibc
    gnupg
    gettext
    coreutils
    libarchive 
    alhp-keyring 
    alhp-mirrorlist
    pacman-mirrorlist
    coreutils
)

makedepends=(
    meson
)

source=(
    pacman.conf
    makepkg.conf
    git+https://gitlab.archlinux.org/pacman/pacman.git
)
b2sums=(
    SKIP
    SKIP
    SKIP
)

provides=('libalpm.so')

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build \
        -D use-git-version=true \
        -D pkg-ext='.pkg.tar.zst' \
        -D src-ext='.src.tar.zst' \
        -D doc=disabled

    meson compile -C build
}


package() {
    DESTDIR="$pkgdir" meson install -C build

    # install Arch specific stuff
    install -dm755 "$pkgdir/etc"
    install -m644 "$srcdir/pacman.conf" "$pkgdir/etc"
    install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"
}
