# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgdesc="Library to aggregates people into metacontacts"
license=(LGPL)

arch=(x86_64)
pkgver=0.15.8+3029+847f64ac
pkgrel=1

url=https://wiki.gnome.org/Projects/Folks

depends=(
	libgee
	libxml2
	evolution-data-server
)

makedepends=(
	vala
	python-dbusmock
	gobject-introspection
)

provides=(
	libfolks.so
	libfolks-{dummy,eds}.so
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/folks.git#branch=master)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	CFLAGS+=" -Wno-error=implicit-function-declaration"
	CFLAGS+=" -Wno-error=incompatible-function-pointer-types"

	local meson_options=(
		-D telepathy_backend=false
		-D tests=false
		-D installed_tests=false
		-D docs=false
	)

	arch-meson $pkgname build "${meson_options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
