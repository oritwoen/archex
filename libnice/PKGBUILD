# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: William DÃ­az <wdiaz@archlinux.us>

pkgname=libnice
pkgdesc="An implementation of the IETF's draft ICE (for p2p UDP data streams)"
license=(LGPL)

arch=(x86_64)
pkgver=0.1.21.1+3049+4d14e613
pkgrel=1

url=https://nice.freedesktop.org

depends=(
	glib2
	gnutls
	gupnp-igd
)

makedepends=(
	graphviz
	gstreamer
	gobject-introspection
)

provides=(libnice.so)

b2sums=(SKIP)
source=(git+https://gitlab.freedesktop.org/libnice/libnice.git#branch=master)

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

prepare() {
	cd $pkgname

	#GUPnP-IGD 1.6
	sed -i 's/gupnp-igd-1\.0/gupnp-igd-1.6/' meson.build
}

build() {
	local options=(
		-D examples=disabled
		-D tests=disabled
	)

	arch-meson $pkgname build "${options[@]}"

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
