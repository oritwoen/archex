# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: William DÃ­az <wdiaz@archlinux.us>

pkgname=libnice
pkgver=0.1.21.1+3041+504189ed
pkgrel=8
pkgdesc="An implementation of the IETF's draft ICE (for p2p UDP data streams)"
url="https://nice.freedesktop.org"
arch=(x86_64)
license=(LGPL)

depends=(
	glib2
	gnutls
	gupnp-igd
)

makedepends=(
	git
	gobject-introspection
	graphviz
	gstreamer
	meson
)

optdepends=('gstreamer: "nice" GStreamer plugin')
provides=(libnice.so)

b2sums=(SKIP)
source=(git+https://gitlab.freedesktop.org/libnice/libnice.git)

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

prepare() {
	cd $pkgname

	#GUPnP-IGD 1.6
	sed -i 's/gupnp-igd-1\.0/gupnp-igd-1.6/' meson.build
}

build() {
	arch-meson $pkgname build \
		-D examples=disabled \
		-D tests=disabled

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
