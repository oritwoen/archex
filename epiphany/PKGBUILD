# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
license=(GPL)

arch=(x86_64)
pkgrel=1
pkgver=46.0+18071+87d5b240d

url=https://wiki.gnome.org/Apps/Web

depends=(
	gtk4
	gcr-4
	libsoup3
	gstreamer
	libadwaita
	libarchive
	webkitgtk-6.0
	libportal-gtk4
)

makedepends=(
	lsb-release
	startup-notification
	gobject-introspection
)

b2sums=(SKIP)
groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/epiphany.git#branch=master)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D tech_preview=true
		-D unit_tests=disabled
	)
	
	arch-meson $pkgname build "${meson_options[@]}"

	meson compile -C build
}

package () {
	meson install -C build --destdir "$pkgdir"
}
