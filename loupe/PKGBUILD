# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=loupe
pkgdesc="A simple image viewer for GNOME"
license=(GPL)

arch=(x86_64)
pkgver=44.3+449+43fa219
pkgrel=1

url="https://gitlab.gnome.org/Incubator/loupe"

depends=(gtk4 libadwaita libgweather-4)
makedepends=(git meson rust itstool)

b2sums=(SKIP)
groups=(gnome)
source=(git+https://gitlab.gnome.org/Incubator/loupe.git)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build
    
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
