# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=loupe
pkgdesc="A simple image viewer for GNOME"
license=(GPL)

arch=(x86_64)
pkgver=45.0+652+1f0df69
pkgrel=2

url="https://gitlab.gnome.org/GNOME/loupe"

depends=(
  cairo
  glib2
  glycin
  graphene
  gtk4
  hicolor-icon-theme
  lcms2
  libadwaita
  libgweather-4
)

makedepends=(
  git
  itstool
  meson
  rust
)

b2sums=(SKIP)
groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/loupe.git#branch=main)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D profile=release
    -D disable-glycin-sandbox=false
	)
	
	arch-meson $pkgname build "${meson_options[@]}"
	meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
