# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgdesc="GNOME application for managing PGP keys."
license=(GPL)

arch=(x86_64)
pkgver=43.0+5860+034ddf2b
pkgrel=42

url="https://wiki.gnome.org/Apps/Seahorse"

depends=(gcr gpgme gtk3 libhandy libpwquality libsecret libsoup3 org.freedesktop.secrets)
makedepends=(git gobject-introspection libldap meson vala yelp-tools)

b2sums=(SKIP)
groups=(gnome-extra)
source=(git+https://gitlab.gnome.org/GNOME/seahorse.git#branch=main)

provides=(x11-ssh-askpass)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    CFLAGS+=" -Wno-error=int-conversion -Wno-error=return-type"

    arch-meson $pkgname build
  
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
