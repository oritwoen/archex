# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-accounts
pkgdesc="Single sign-on framework for GNOME"
license=(LGPL)

arch=(x86_64)
pkgver=3.49.2+2955+6d1c7368
pkgrel=1

url=https://wiki.gnome.org/Projects/GnomeOnlineAccounts

depends=(
	gcr
	krb5
	librest
	libsecret
	json-glib
	libnotify
	webkit2gtk-4.1
)

makedepends=(
	vala
	gobject-introspection
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/gnome-online-accounts.git#branch=master)

provides=(
	libgoa
	libgoa-1.0.so
	libgoa-backend-1.0.so
)

replaces=(libgoa)
conflicts=(libgoa)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)
	
	echo ${_version}+${_commits}+${_commith}
}

build() {
	arch-meson $pkgname build "${options[@]}"
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
