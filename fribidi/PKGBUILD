# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Nezmer <Nezmer@gmail.com>

pkgname=fribidi
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm"
license=(LGPL)

arch=(x86_64)
pkgver=1.0.13+422+5b9a242
pkgrel=1

url="https://github.com/fribidi/fribidi"

depends=(
	glibc
)

b2sums=(SKIP)
source=(git+https://github.com/fribidi/fribidi.git#branch=master)
provides=(libfribidi.so)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $6}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D deprecated=false
		-D docs=false
		-D tests=false
	)
	
	arch-meson $pkgname build "${options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
