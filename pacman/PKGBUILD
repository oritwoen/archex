# Maintainer: Dominik Opyd <dominik.opyd@gmail.com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Morten Linderud <foxboron@archlinux.org>

pkgname=pacman
pkgdesc="A library-based package manager with dependency support"
license=(GPL)

arch=(x86_64)
pkgver=7.0.0+7418+6ba5c20e
pkgrel=2

url="https://www.archlinux.org/pacman"

depends=(
	curl
	bash
	grep
	gawk
	gpgme
	glibc
	gnupg
	gettext
	coreutils
	libarchive

	# repos
	alhp-keyring
	alhp-mirrorlist
	pacman-mirrorlist

	# devtools
	git
	meson
	cmake

	# makepkg
	mold
	pigz
	polly
	clang
	libc++
	pbzip2
	ccache
	openmp
	sccache
)

b2sums=(
	SKIP 
	SKIP
	SKIP
)
source=(
	pacman.conf
	makepkg.conf
	git+https://gitlab.archlinux.org/pacman/pacman.git
)

provides=('libalpm.so')

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)
	
	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D use-git-version=true
		-D pkg-ext='.pkg.tar.zst'
		-D scriptlet-shell=/usr/bin/bash
		-D ldconfig=/usr/bin/ldconfig
		-D doc=disabled
	)
	
	arch-meson $pkgname build "${options[@]}"
	
	meson compile -C build
}


package() {
	local unit
	local wantsdir="$pkgdir/usr/lib/systemd/system/sockets.target.wants"

	meson install -C build --destdir "$pkgdir"

	# install Arch specific stuff
	install -dm755 "$pkgdir/etc"
	install -dm755 "$wantsdir"
	
	install -m644 "$srcdir/pacman.conf" "$pkgdir/etc"
	install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"

	for unit in dirmngr gpg-agent gpg-agent-{browser,extra,ssh} keyboxd; do
		ln -s "../${unit}@.socket" "$wantsdir/${unit}@etc-pacman.d-gnupg.socket"
	done
}
