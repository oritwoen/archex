# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libshumate
pkgdesc="Map widget for GTK 4"
license=(LGPL)

arch=(x86_64)
pkgver=1.2.alpha+558+7f6d22b
pkgrel=2

url="https://wiki.gnome.org/Projects/libshumate"

depends=(
	cairo
	glib2
	gtk4
	libsoup3
	sqlite
)

makedepends=(
	git
	gobject-introspection
	meson
	vala
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/libshumate.git)

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D gtk_doc=false
		-D vector_renderer=true
		-D sysprof=disabled
	)
	
	arch-meson $pkgname build "${meson_options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
