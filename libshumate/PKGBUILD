# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libshumate
pkgdesc="Map widget for GTK 4"
license=(LGPL)

arch=(x86_64)
pkgver=1.2.rc+610+8837c53
pkgrel=1

url=https://wiki.gnome.org/Projects/libshumate

depends=(
	gtk
	glib2
	cairo
	pango
	sqlite
	graphene
	libsoup3
	json-glib
	protobuf-c
	gdk-pixbuf2
)

makedepends=(
	vala
	gobject-introspection
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/libshumate.git#branch=main)

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D gtk_doc=false
		-D sysprof=disabled
	)
	
	arch-meson $pkgname build "${options[@]}"

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
