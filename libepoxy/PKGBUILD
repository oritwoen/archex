# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=libepoxy
pkgdesc="Library handling OpenGL function pointer management"
license=(MIT)

arch=(x86_64)
pkgver=1.5.11+591+2df68f8
pkgrel=6

url="https://github.com/anholt/libepoxy"

depends=(glibc libglvnd libx11)
makedepends=(git meson mesa-libgl systemd doxygen graphviz)

source=(git+https://github.com/anholt/libepoxy#branch=master)
b2sums=(SKIP)

provides=(libepoxy.so)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $6}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D docs=false
		-D glx=yes
		-D x11=true
		-D tests=false
	)
	
	arch-meson $pkgname build "${meson_options[@]}"
	
	meson compile -C build
}


package() {
	meson install -C build --destdir "$pkgdir"
	
	install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libepoxy/COPYING
}
