# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=graphene
pkgdesc="A thin layer of graphic data types"
license=(MIT)

arch=(x86_64)
pkgver=1.11.1+1253+d03fb3d
pkgrel=1

url=https://ebassi.github.io/graphene

makedepends=(
	glib2
	gobject-introspection
)

b2sums=(SKIP)
source=(git+https://github.com/ebassi/graphene.git#branch=master)

provides=(libgraphene-1.0.so)

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D arm_neon=false
		-D tests=false
		-D installed_tests=false
	)

	arch-meson $pkgname build "${options[@]}"

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
	
	install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 graphene/LICENSE.txt
}
