# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=graphene
pkgver=1.11.1+1246+ba99345
pkgrel=13
pkgdesc="A thin layer of graphic data types"
url="https://ebassi.github.io/graphene/"
arch=(x86_64)
license=(MIT)

makedepends=(git gtk-doc gobject-introspection meson glib2)

b2sums=(SKIP)
source=(git+https://github.com/ebassi/graphene.git#branch=master)

provides=(libgraphene-1.0.so)

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D gtk_doc=false
		-D gobject_types=true
		-D introspection=enabled
		-D gcc_vector=true
		-D sse2=true
		-D arm_neon=false
		-D tests=false
		-D installed_tests=false
	)

	arch-meson $pkgname build "${meson_options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
	install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 graphene/LICENSE.txt
}
