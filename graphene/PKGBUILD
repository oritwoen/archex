# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=graphene
pkgver=1.11.1+1244+0cfa05f
pkgrel=21
pkgdesc="A thin layer of graphic data types"
url="https://ebassi.github.io/graphene/"
arch=(x86_64)
license=(MIT)

makedepends=(git gtk-doc gobject-introspection meson glib2)

b2sums=(SKIP)
source=(git+https://github.com/ebassi/graphene.git)

provides=(libgraphene-1.0.so)

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	arch-meson $pkgname build \
		-D gtk_doc=true \
		-D tests=false \
		-D installed_tests=false

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
	install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 graphene/LICENSE.txt
}
