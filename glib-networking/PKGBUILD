# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=glib-networking
pkgdesc="Network extensions for GLib"
license=(GPL2)

arch=(x86_64)
pkgver=2.78.0+1903+fab1aec
pkgrel=10

url="https://gitlab.gnome.org/GNOME/glib-networking"

depends=(
  glib2
  gnutls
  gsettings-desktop-schemas
  libproxy
)

makedepends=(
  git
  meson
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/glib-networking.git)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build
    
    meson compile -C build
}
package() {
    meson install -C build --destdir "$pkgdir"
}
