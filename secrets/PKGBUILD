# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=secrets
pkgdesc='A password manager which integrates perfectly with the GNOME.'
license=(GPL3)

arch=(any)
pkgver=8.0+3959+d2897f05
pkgrel=1

url=https://gitlab.gnome.org/world/secrets

depends=(gtk4 libadwaita libpwquality python-gobject python-pykeepass python-pyotp python-cairo python-validators python-zxcvbn)
makedepends=(git gobject-introspection meson)

b2sums=(SKIP)
groups=(gnome-extra)
source=(git+https://gitlab.gnome.org/world/secrets.git)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build
    
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
