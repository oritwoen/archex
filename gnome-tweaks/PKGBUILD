# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-tweaks
pkgdesc="Graphical interface for advanced GNOME 3 settings (Tweak Tool)"
license=(GPL-3.0-or-later)

arch=(any)
pkgver=46.beta+2096+a53f2d2
pkgrel=1

url=https://wiki.gnome.org/Apps/Tweaks

depends=(
	libhandy
	python-gobject
	gnome-settings-daemon
)

b2sums=(SKIP)
groups=(gnome-extra)
source=(git+https://gitlab.gnome.org/GNOME/gnome-tweaks.git#branch=master)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	arch-meson $pkgname build

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
