# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=libdecor
pkgdesc='Client-side decorations library for Wayland clients'
license=(MIT)

arch=(x86_64)
pkgver=0.2.2+287+8dc6b62
pkgrel=1

url=https://gitlab.freedesktop.org/libdecor/libdecor

depends=(
	gtk3
	dbus
	pango
	wayland
)

makedepends=(
	wayland-protocols
)

b2sums=(SKIP)
source=(git+https://gitlab.freedesktop.org/libdecor/libdecor.git#branch=master)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D demo=false
	)
	
	arch-meson $pkgname build "${options[@]}"
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "${pkgdir}"
	
	install -Dm644 "${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
