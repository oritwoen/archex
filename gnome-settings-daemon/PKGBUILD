# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-settings-daemon
pkgdesc="GNOME Settings Daemon"
license=(
	GPL-2.0-or-later
	LGPL-2.0-or-later
)


arch=(x86_64)
pkgver=46.beta+6697+30bede3e
pkgrel=1

url=https://gitlab.gnome.org/GNOME/gnome-settings-daemon

depends=(
	gcr
	nss
	gtk3
	dconf
	libnm
	upower
	systemd
	geoclue
	librsvg
	libwacom
	libgudev
	libnotify
	libgweather-4
	gnome-desktop
	geocode-glib-2
	libcanberra-pulse
	pulse-native-provider
	gsettings-desktop-schemas
)

makedepends=(
	python
	libxslt
	usbguard
	python-packaging
)

b2sums=(
	SKIP
	SKIP
)

groups=(gnome)
source=(
	git+https://gitlab.gnome.org/GNOME/gnome-settings-daemon.git#branch=master
	git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git#branch=master
)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

prepare() {
	cd $pkgname

	git submodule init
	git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
	git -c protocol.file.allow=always submodule update
}


build() {
	arch-meson $pkgname build

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
