# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-settings-daemon
pkgdesc="GNOME Settings Daemon"
license=(GPL)

arch=(x86_64)
pkgver=45.0+6677+2beefa73
pkgrel=5

url="https://gitlab.gnome.org/GNOME/gnome-settings-daemon"

depends=(
  dconf
  gcr-4
  geoclue
  geocode-glib-2
  gnome-desktop
  gsettings-desktop-schemas
  gtk3
  libcanberra-pulse
  libgudev
  libgweather-4
  libnm
  libnotify
  librsvg
  libwacom
  nss
  pulseaudio
  pulseaudio-alsa
  systemd
  upower
)

makedepends=(
  docbook-xsl
  git
  libxslt
  meson
  python
  usbguard
)

b2sums=(SKIP)
groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/gnome-settings-daemon.git#branch=master)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}


build() {
    local meson_options=(
        -D systemd=true
        -D alsa=true
        -D gudev=true
        -D cups=true
        -D network_manager=true
        -D rfkill=true
        -D smartcard=false
        -D usb-protection=true
        -D wayland=true
        -D wwan=false
        -D gcr3=false
        -D colord=true
    )
    
    arch-meson $pkgname build "${meson_options[@]}"
    
    meson compile -C build
}

package() {
    DESTDIR="$pkgdir" meson install -C build
}
