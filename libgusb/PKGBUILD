# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgusb
pkgdesc='GObject wrapper for libusb1'

arch=(x86_64)
pkgver=0.4.9+476+8bef41c
pkgrel=2

url=https://github.com/hughsie/libgusb

license=(LGPL2.1)

depends=(
	glib2
	libusb
	json-glib
)

makedepends=(
	vala
	umockdev
	python-setuptools
	gobject-introspection
)

b2sums=(SKIP)
source=(git+https://github.com/hughsie/libgusb.git#branch=main)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D tests=false
		-D docs=false
	)

	arch-meson $pkgname build "${options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
