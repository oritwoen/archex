# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgusb
pkgver=0.4.8+467+f2df58f
pkgrel=3
pkgdesc='GObject wrapper for libusb1'
url=https://github.com/hughsie/libgusb
arch=(x86_64)
license=(LGPL2.1)

depends=(
	glib2
	json-glib
	libusb
)

makedepends=(
	git
	gobject-introspection
	meson
	python-setuptools
	umockdev
	vala
)

b2sums=(SKIP)
source=(git+https://github.com/hughsie/libgusb.git#branch=main)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D tests=false
		-D vapi=true
		-D usb_ids=/usr/share/hwdata/usb.ids
		-D docs=false
		-D introspection=true
		-D umockdev=disabled
	)

	arch-meson $pkgname build "${meson_options[@]}"
	meson compile -C build
}

package() {
	DESTDIR="${pkgdir}" ninja -C build install
}
