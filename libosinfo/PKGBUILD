# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=libosinfo
pkgver=1.11.0+1944+41778d2
pkgrel=23
pkgdesc='GObject based library API for managing information about operating systems, hypervisors and the (virtual) hardware devices they can support'
arch=('x86_64')
url='https://libosinfo.org/'
license=('GPL' 'LGPL')

depends=('gcc-libs' 'glib2' 'glibc' 'hwdata' 'libsoup3' 'libxml2' 'libxslt' 'osinfo-db')
makedepends=('gobject-introspection' 'meson' 'vala')

b2sums=(SKIP)
source=(git+https://gitlab.com/libosinfo/libosinfo.git)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D enable-gtk-doc=false
		-D enable-tests=false
		-D libsoup-abi=3.0
		-D with-usb-ids-path=/usr/share/hwdata/usb.ids
		-D with-pci-ids-path=/usr/share/hwdata/pci.ids
	)
	
	arch-meson $pkgname build "${meson_options[@]}"
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
