# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=libosinfo
pkgdesc='GObject based library API for managing information about operating systems, hypervisors and the (virtual) hardware devices they can support'
license=('GPL' 'LGPL')

arch=(x86_64)
pkgver=1.12.0+1947+5bbdd06
pkgrel=2

url=https://libosinfo.org/

depends=(
	glib2
	glibc
	hwdata
	libxslt
	libxml2
	libsoup3
	gcc-libs
	osinfo-db
)

makedepends=(
	vala
	gobject-introspection
)

b2sums=(SKIP)
source=(git+https://gitlab.com/libosinfo/libosinfo.git#branch=main)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D enable-gtk-doc=false
		-D enable-tests=false
		-D with-pci-ids-path=/usr/share/hwdata/pci.ids
		-D with-usb-ids-path=/usr/share/hwdata/usb.ids
	)
	
	arch-meson $pkgname build "${options[@]}"
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
