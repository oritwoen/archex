# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libei
pkgdesc="Library for Emulated Input"
license=(MIT)

arch=(x86_64)
pkgver=1.2.1+1018+08f1d41
pkgrel=1

url=https://libinput.pages.freedesktop.org/libei/

depends=(
	systemd
	libevdev
)

makedepends=(
	libxkbcommon
	
	python-yaml
	python-jinja
	python-attrs
	python-pytest
	python-dbusmock
	python-structlog
)

provides=(
	lib{ei,eis,oeffis}.so
)

b2sums=(
	SKIP 
	SKIP
)

source=(
	git+https://github.com/nemequ/munit.git
	git+https://gitlab.freedesktop.org/libinput/libei.git
)

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

prepare() {
	cd $pkgname
	
	ln -sr ../munit subprojects/munit
}

build() {
	local options=(
		-D tests=disabled
	)
	
	arch-meson $pkgname build "${options[@]}"
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir" --skip-subprojects
	
	install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libei/COPYING
}
