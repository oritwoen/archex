# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=(gvfs)
pkgdesc="Virtual filesystem implementation for GIO"
license=(LGPL-2.0-only)

arch=(x86_64)
pkgver=1.53.91+6450+435047ae
pkgrel=1

url="https://wiki.gnome.org/Projects/gvfs"

depends=(
	gcr
	avahi
	dconf
	fuse3
	psmisc
	udisks2
	gsettings-desktop-schemas

	libgudev
	libsoup3
	libbluray
	libsecret
	libarchive
	libcdio-paranoia
)

makedepends=(
	gtk3
	dbus
	libmtp
	libgoa
	libnfs
	python
	msgraph
	openssh
	libgdata
	smbclient
	libgphoto2
	libimobiledevice
)

groups=(gnome)
b2sums=(
	SKIP
	SKIP
)
source=(
	gvfsd.hook
	git+https://gitlab.gnome.org/GNOME/gvfs.git#branch=master
)

provides=(gvfs-{smb,afc,gphoto2,goa,mtp,nfs,google})
replaces=(gvfs-{smb,afc,gphoto2,goa,mtp,nfs,google})
conflicts=(gvfs-{smb,afc,gphoto2,goa,mtp,nfs,google})

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	arch-meson $pkgname build
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"

	install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
}
