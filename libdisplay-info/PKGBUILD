# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Simon Ser <contact@emersion.fr>

pkgname=libdisplay-info
pkgdesc='EDID and DisplayID library'
license=(MIT)

arch=(x86_64)
pkgver=0.2.0+dev+231+ae6cb52
pkgrel=4

url="https://gitlab.freedesktop.org/emersion/libdisplay-info"

depends=(glibc)
makedepends=(
	hwdata
	meson
	ninja
	python
)

provides=(libdisplay-info.so)

b2sums=(SKIP)
source=(git+https://gitlab.freedesktop.org/emersion/libdisplay-info.git#branch=main)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

    echo ${_version/-/+}+${_commits}+${_commith}
}

build() {
	arch-meson $pkgname build
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "${pkgdir}"
	
	install -Dm644 "${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
