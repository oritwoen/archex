# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=iputils
pkgdesc="Network monitoring tools, including ping"
license=(
	BSD-3-Clause
	GPL-2.0-or-later
)

arch=(x86_64)
pkgver=20240117+1198+3400f3a
pkgrel=1

url=https://github.com/iputils/iputils

depends=(
	libcap
	libidn2
)

makedepends=(
	systemd
	iproute
	perl-sgmls
)

b2sums=(SKIP)
source=(git+https://github.com/iputils/iputils.git#branch=master)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 2 version meson.build | awk -F"'" 'NR==2 {print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D BUILD_MANS=false
		-D SKIP_TESTS=true
	)
	
	arch-meson $pkgname build "${options[@]}"
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
