# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=librest
pkgdesc="Library for accessing RESTful web services"
license=(LGPL)

arch=(x86_64)
pkgver=0.9.1+687+5970778
pkgrel=30

url="https://gitlab.gnome.org/GNOME/librest"

depends=(glib2 libxml2 libsoup3 json-glib)
makedepends=(gobject-introspection git meson gi-docgen vala libadwaita gtksourceview5)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/librest.git#branch=master)
provides=(librest{,-extras}-1.0.so)

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D ca_certificates=false
		-D introspection=true
		-D vapi=true
		-D examples=false
		-D gtk_doc=false
		-D soup2=false
		-D tests=false
	)
	
	arch-meson $pkgname build "${meson_options[@]}"
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
