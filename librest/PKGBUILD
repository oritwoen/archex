# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=librest
pkgdesc="Library for accessing RESTful web services"
license=(LGPL)

arch=(x86_64)
pkgver=0.9.1+703+23f2d27
pkgrel=1

url=https://gitlab.gnome.org/GNOME/librest

depends=(
	glib2 
	libxml2
	libsoup3
	json-glib
)

makedepends=(
	vala
	libadwaita
	gtksourceview5
	gobject-introspection
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/librest.git#branch=master)
provides=(librest{,-extras}-1.0.so)

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D vapi=true
		-D examples=false
		-D gtk_doc=false
		-D tests=false
	)
	
	arch-meson $pkgname build "${options[@]}"
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
