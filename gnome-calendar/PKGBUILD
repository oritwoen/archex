# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
license=(GPL)

arch=(x86_64)
pkgver=45.rc+4508+d8742df7
pkgrel=2

url="https://wiki.gnome.org/Apps/Calendar"

depends=(
  evolution-data-server
  geoclue
  geocode-glib-2
  gsettings-desktop-schemas
  libadwaita
  libedataserverui4
  libgweather-4
  libsoup3
)
makedepends=(
  git
  meson
)

groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/gnome-calendar.git#branch=main)
b2sums=('SKIP')

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build

    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
