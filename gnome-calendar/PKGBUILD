# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
license=(GPL)

arch=(x86_64)
pkgver=46.alpha+4649+fccb0ee0
pkgrel=1

url=https://wiki.gnome.org/Apps/Calendar

depends=(
	gtk4
	dconf
	glib2
	pango
	libical
	geoclue
	libsoup3
	graphene
	libadwaita
	libgweather-4
	libedataserverui4
	hicolor-icon-theme
	evolution-data-server
	gsettings-desktop-schemas
)

b2sums=(SKIP)
groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/gnome-calendar.git#branch=main)


pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	arch-meson $pkgname build
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
