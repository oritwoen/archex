# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
license=(GPL)

arch=(x86_64)
pkgver=46.alpha+4574+f83d81da
pkgrel=3

url="https://wiki.gnome.org/Apps/Calendar"

depends=(
  dconf
  evolution-data-server
  geoclue
  geocode-glib-2
  glib2
  graphene
  gsettings-desktop-schemas
  gtk4
  hicolor-icon-theme
  libadwaita
  libedataserverui4
  libgweather-4
  libical
  libsoup3
  pango
)

makedepends=(
  git
  meson
)

groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/gnome-calendar.git)
b2sums=('SKIP')

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D tracing=false
    -D profile=default
	)

	arch-meson $pkgname build "${meson_options[@]}"
	meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
