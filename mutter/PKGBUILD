# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Michael Kanis <mkanis_at_gmx_dot_de>

pkgname=mutter
pkgdesc="Window manager and compositor for GNOME"
license=(GPL)

arch=(x86_64)
pkgrel=1
pkgver=45.0+30840+f2c55d647

url="https://gitlab.gnome.org/GNOME/mutter"

depends=(
  colord
  dconf
  gnome-desktop-4
  gnome-settings-daemon
  graphene
  gsettings-desktop-schemas
  iio-sensor-proxy
  lcms2
  libcanberra
  libdisplay-info
  libgudev
  libinput
  libei
  libsm
  libsysprof-capture
  libxkbcommon-x11
  libxkbfile
  pipewire
  startup-notification
  xorg-xwayland
)

makedepends=(
  egl-wayland
  gi-docgen
  git
  gobject-introspection
  gtk3
  meson
  sysprof
  wayland-protocols
  xorg-server
  xorg-server-xvfb
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/mutter.git)

provides=("libmutter-13.so=0-64")

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    local meson_options=(
      -D opengl=true
      -D gles2=true
      -D egl=true
      -D glx=true
      -D wayland=true
      -D xwayland=true
      -D systemd=true
      -D native_backend=true
      -D remote_desktop=true
      -D libgnome_desktop=true
      -D egl_device=false
      -D wayland_eglstream=false
      -D udev=true
      -D libwacom=true
      -D sound_player=true
      -D pango_ft2=true
      -D startup_notification=true
      -D sm=true
      -D introspection=true
      -D docs=false
      -D cogl_tests=false
      -D clutter_tests=false
      -D core_tests=false
      -D native_tests=false
      -D tests=false
      -D kvm_tests=false
      -D tty_tests=false
      -D profiler=false
      -D installed_tests=false
      -D verbose=true
      -D catch=false
      -D libdisplay_info=true
    )

    arch-meson mutter build "${meson_options[@]}"
  
    meson compile -C build
}


package() {
    meson install -C build --destdir "$pkgdir"
}
