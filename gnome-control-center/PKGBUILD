# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-control-center
pkgdesc="GNOME's main interface to configure various aspects of the desktop"
license=(GPL2)

arch=(x86_64)
pkgver=46.alpha+23014+bfe5dec52
pkgrel=3

url="https://gitlab.gnome.org/GNOME/gnome-control-center"

depends=(
  accountsservice
  bolt
  cairo
  colord-gtk4
  cups-pk-helper
  dconf
  fontconfig
  gcr
  gdk-pixbuf2
  glib2
  gnome-bluetooth-3.0
  gnome-color-manager
  gnome-desktop-4
  gnome-online-accounts
  gnome-settings-daemon
  gnome-shell
  gnutls
  gsettings-desktop-schemas
  gsound
  gtk3
  gtk4
  hicolor-icon-theme
  krb5
  libadwaita
  libcolord
  libcups
  libepoxy
  libgoa
  libgtop
  libgudev
  libibus
  libmalcontent
  libmm-glib
  libnm
  libnma-gtk4
  libpulse
  libpwquality
  libsecret
  libwacom
  libx11
  libxi
  libxml2
  pango
  polkit
  smbclient
  sound-theme-freedesktop
  tecla
  udisks2
  upower
)

makedepends=(git meson python modemmanager)

b2sums=(SKIP SKIP)
groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/gnome-control-center.git#branch=main
        git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git)

options=(!lto)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

prepare() {
  cd $pkgname

  git submodule init subprojects/gvc
  git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
  git -c protocol.file.allow=always submodule update
}


build() {
    local meson_options=(
        -D deprecated-declarations=disabled
        -D documentation=false
        -D ibus=true
        -D privileged_group=wheel
        -D snap=true
        -D tests=false
        -D wayland=true
        -D profile=default
        -D malcontent=true
    )
    
    arch-meson $pkgname build "${meson_options[@]}"
    
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"

    install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"
}

