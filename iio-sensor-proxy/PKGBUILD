# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Eric Lehmann <katyl@katyl.info>
# Contributor: Thomas Fanninger <thomas@fanninger.at>
# Contributor: ultraviolet <ultravioletnanokitty@gmail.com>
# Contributor: Pablo Lezeta <prflr88@gmail.com>

pkgname=iio-sensor-proxy
pkgdesc='IIO accelerometer sensor to input device proxy'
license=('GPL2')

arch=('x86_64')
pkgver=3.5+492+29c79c9
pkgrel=7

url='https://gitlab.freedesktop.org/hadess/iio-sensor-proxy/'

depends=('systemd' 'libgudev' 'glib2' 'polkit')
makedepends=('gtk3' 'meson')

b2sums=(SKIP)
source=(git+https://gitlab.freedesktop.org/hadess/iio-sensor-proxy.git#branch=master)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D udevrulesdir=auto
		-D systemdsystemunitdir=auto
		-D tests=false
		-D gtk-tests=false
		-D geoclue-user=geoclue
		-D gtk_doc=false
	)
	
	arch-meson $pkgname build "${meson_options[@]}"
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
