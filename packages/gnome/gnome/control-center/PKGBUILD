pkgname=gnome-control-center

arch=(x86_64)
pkgver=48.alpha+24533+c73de3d53
pkgrel=4

b2sums=(
	SKIP 
	SKIP
)

source=(
	git+https://gitlab.gnome.org/gnome/gnome-control-center.git
	git+https://gitlab.gnome.org/gnome/libgnome-volume-control.git
)

provides=(gnome-keybindings)
replaces=(gnome-keybindings)
conflicts=(gnome-keybindings)

#options=(!lto)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

prepare() {
	cd $pkgname

	git submodule init subprojects/gvc
	git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
	git -c protocol.file.allow=always -c protocol.allow=never submodule update
}

build() {
	local options=(
		-D tests=false
	)

	arch-meson $pkgname build "${options[@]}"

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}

