pkgname=gjs

arch=(x86_64)
pkgver=1.82.0+5637+50723b98
pkgrel=3

b2sums=(SKIP SKIP)
source=(
	git+https://gitlab.gnome.org/gnome/gjs.git
	git+https://gitlab.gnome.org/GNOME/gobject-introspection-tests.git
)
provides=(libgjs.so)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 "version:" meson.build | cut -d "'" -f 8)
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

prepare() {
	cd $pkgname

	git submodule init
	git submodule set-url subprojects/gobject-introspection-tests "${srcdir}/gobject-introspection-tests"
	git -c protocol.file.allow=always -c protocol.allow=never submodule update
}


build() {
	local options=(
		-D profiler=disabled
		-D installed_tests=false
		-D skip_dbus_tests=true
		-D skip_gtk_tests=true
	)

	arch-meson $pkgname build "${options[@]}"

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
