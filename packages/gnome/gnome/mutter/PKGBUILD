pkgname=mutter

arch=(x86_64)
pkgrel=3
pkgver=47.0+32716+f251e1f491

b2sums=(SKIP SKIP)
source=(
	git+https://gitlab.gnome.org/gnome/gvdb.git
	git+https://gitlab.gnome.org/gnome/mutter.git
)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D x11=false
		-D cogl_tests=false
		-D clutter_tests=false
		-D mutter_tests=false
		-D tests=disabled
		-D profiler=false
		-D installed_tests=false
	)

	# Inject gvdb
	export MESON_PACKAGE_CACHE_DIR="$srcdir"

	arch-meson mutter build "${options[@]}"

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
