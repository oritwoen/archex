pkgname=colord

arch=(x86_64)
pkgver=1.4.8+1904+4a5a8ac
pkgrel=42

b2sums=(SKIP)
source=(git+https://github.com/hughsie/colord.git)

provides=(colord-sane libcolord)
replaces=(colord-sane libcolord)
conflicts=(colord-sane libcolord)

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)
	
	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D libcolordcompat=true
		-D sane=true
		-D vapi=true
		-D print_profiles=true
		-D tests=false
		-D daemon_user=colord
		-D man=false
		-D docs=false
	)

	arch-meson $pkgname build "${options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"

	echo 'u colord - "Color management daemon" /var/lib/colord' |
		install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/colord.conf"
}
