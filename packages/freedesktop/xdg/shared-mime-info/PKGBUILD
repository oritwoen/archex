pkgname=shared-mime-info

arch=(x86_64)
pkgver=2.4+2031+815b520
pkgrel=48

b2sums=(
	SKIP 
	SKIP
)

source=(
	update-mime-database.hook 
	git+https://gitlab.freedesktop.org/xdg/shared-mime-info.git
)
install=shared-mime-info.install

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D build-tests=false
	)
	
	arch-meson $pkgname build "${options[@]}"
	
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
	
	# use a prefix to run it early enough - other hooks may depend on it - FS#72858
  	install -Dm644 "$srcdir"/update-mime-database.hook "$pkgdir"/usr/share/libalpm/hooks/30-update-mime-database.hook
}
