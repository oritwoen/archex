pkgname=orc

arch=(x86_64)
pkgver=0.4.40.1+1796+06d19aa
pkgrel=5

b2sums=(SKIP)
source=(git+https://gitlab.freedesktop.org/gstreamer/orc.git)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $6}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D orc-test=disabled
		-D benchmarks=disabled
		-D examples=disabled
		-D gtk_doc=disabled
		-D tests=disabled
	)
	
	arch-meson $pkgname build "${meson_options[@]}"
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
	
	install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 orc/COPYING
}
