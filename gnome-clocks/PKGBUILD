# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-clocks
pkgdesc="Clocks applications for GNOME"
license=(GPL)

arch=(x86_64)
pkgver=45.0+2711+0aede17
pkgrel=7

url="https://wiki.gnome.org/Apps/Clocks"

depends=(
    gtk4
    geoclue
    libadwaita
    libgweather-4
    geocode-glib-2
    gnome-desktop-4
    xdg-desktop-portal
)

makedepends=(git vala meson yelp-tools gobject-introspection)

b2sums=(SKIP)
groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/gnome-clocks.git#branch=master)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
        -D profile=default
        -D docs=false
	)

	arch-meson $pkgname build "${meson_options[@]}"
	meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
