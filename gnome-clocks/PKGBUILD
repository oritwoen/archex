# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-clocks
pkgdesc="Clocks applications for GNOME"
license=(GPL)

arch=(x86_64)
pkgver=44.0+2645+2a0ff6d
pkgrel=2

url="https://wiki.gnome.org/Apps/Clocks"

depends=(
    gtk4
    gsound
    geoclue
    libadwaita
    libgweather-4
    geocode-glib-2
    gnome-desktop-4
)

makedepends=(git vala meson yelp-tools gobject-introspection)

b2sums=(SKIP)
groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/gnome-clocks.git#branch=master)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build

    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
