# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgdesc='A trivial-to-use personal backup tool to protect against accidental data loss'
license=(GPL)

arch=(x86_64)
pkgver=46.0+3255+80f1b6e4
pkgrel=1

url=https://wiki.gnome.org/Apps/DejaDup

depends=(
	gvfs
	rclone
	libsecret
	duplicity
	libadwaita
	python-gobject
	python-pydrive2
	org.freedesktop.secrets
)

makedepends=(
	vala
	itstool
	appstream-glib
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/World/deja-dup.git#branch=main)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 2 version meson.build | awk -F"'" 'NR==2 {print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	CFLAGS+=" -Wno-error=incompatible-function-pointer-types"

	local options=(
		-D enable_restic=true
		-D enable_borg=true
	)

	arch-meson $pkgname build "${options[@]}"

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
