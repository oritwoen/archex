# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgdesc='A trivial-to-use personal backup tool to protect against accidental data loss'
license=('GPL')

arch=('x86_64')
pkgver=45.2+3185+e3f42bed
pkgrel=5

url='https://wiki.gnome.org/Apps/DejaDup'

depends=(
  'duplicity'
  'gvfs'
  'libsecret'
  'org.freedesktop.secrets'
  'python-gobject'
  'python-pydrive2'
  'libadwaita'
  'rclone'
)

makedepends=(
  'appstream-glib'
  'itstool'
  'meson'
  'vala'
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/World/deja-dup.git)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 2 version meson.build | awk -F"'" 'NR==2 {print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
  	CFLAGS+=" -Wno-error=incompatible-function-pointer-types"

    arch-meson $pkgname build \
      -D enable_restic=true \
      -D enable_borg=true
  
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
