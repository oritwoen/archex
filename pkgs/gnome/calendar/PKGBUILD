pkgname=gnome-calendar

arch=(x86_64)
pkgver=49.alpha+5225+05eb4651
pkgrel=1

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/gnome/gnome-calendar.git)

depends=(
  geoclue
  gsettings-desktop-schemas

  libical
  libadwaita
  libedataserverui4
)

makedepends=(
  git
  meson
  glib2-devel
  blueprint-compiler

  mold
  clang
)

pkgver() {
  cd ${pkgname}

  local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
  local _commits=$(git rev-list --count HEAD)
  local _commith=$(git rev-parse --short HEAD)

  echo ${_version}+${_commits}+${_commith}
}

build() {
  arch-meson ${pkgname} build
  meson compile -C build
}

package() {
  meson install -C build --destdir ${pkgdir}
}
