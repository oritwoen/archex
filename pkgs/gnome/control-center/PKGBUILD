pkgname=gnome-control-center

arch=(x86_64)
pkgver=49.alpha+25191+61c60f9d6
pkgrel=1

b2sums=(SKIP SKIP)
source=(
  git+https://gitlab.gnome.org/gnome/libgxdp.git
  git+https://gitlab.gnome.org/gnome/gnome-control-center.git
)

provides=(gnome-keybindings)
replaces=(gnome-keybindings)
conflicts=(gnome-keybindings)

depends=(
  gtk4
  tecla
  colord
  udisks2
  smbclient
  colord-gtk4
  accountsservice

  libgoa
  libibus
  libgtop
  libpulse
  libcolord
  libadwaita
  libnma-gtk4
  libpwquality

  gnome-desktop-4
  gnome-bluetooth
  gnome-color-manager
  gnome-settings-daemon
  gnome-online-accounts
)

makedepends=(
  git
  meson
  glib2-devel

  mold
  clang
)

pkgver() {
  cd ${pkgname}

  local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
  local _commits=$(git rev-list --count HEAD)
  local _commith=$(git rev-parse --short HEAD)

  echo ${_version}+${_commits}+${_commith}
}

build() {
  local options=(
    -D tests=false
  )

  # Inject libgxdp
  export MESON_PACKAGE_CACHE_DIR=${srcdir}

  arch-meson ${pkgname} build ${options[@]}

  meson compile -C build
}

package() {
  meson install -C build --destdir ${pkgdir}
}
