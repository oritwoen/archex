pkgname=glib

arch=(x86_64)
pkgver=2.83.0+30888+cc2b78ec4
pkgrel=15

b2sums=(
	SKIP
	SKIP
	SKIP
	SKIP
)

source=(
	gio-querymodules.hook
	glib-compile-schemas.hook
	git+https://gitlab.gnome.org/gnome/glib.git
	git+https://gitlab.gnome.org/gnome/gvdb.git
)

replaces=(glib2-devel glib2)
conflicts=(glib2-devel glib2)
provides=(libg{lib,io,module,object,thread}-2.0.so=0-64 glib2 glib2-devel)

makedepends=(
	git
	meson
	gobject-introspection
)

pkgver() {
	cd glib

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

prepare() {
	cd glib
	
	git submodule init
	git submodule set-url subprojects/gvdb "$srcdir/gvdb"
	git -c protocol.file.allow=always -c protocol.allow=never submodule update
}

build() {
	local options=(
		-D selinux=disabled
		-D man-pages=disabled
		-D dtrace=disabled
		-D systemtap=disabled
		-D sysprof=disabled
		-D tests=false
		-D glib_debug=disabled
	)
	
	arch-meson glib build "${options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
	
	install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
	touch "$pkgdir/usr/lib/gio/modules/.keep"

	python -m compileall -d /usr/share/glib-2.0/codegen \
		"$pkgdir/usr/share/glib-2.0/codegen"
	
	python -O -m compileall -d /usr/share/glib-2.0/codegen \
		"$pkgdir/usr/share/glib-2.0/codegen"
}
