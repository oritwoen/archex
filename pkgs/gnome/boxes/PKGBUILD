pkgname=gnome-boxes

arch=(x86_64)
pkgver=49.alpha+6361+5cbde9c4
pkgrel=1

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/gnome/gnome-boxes.git)

depends=(
  gtk3
  gtk4
  spice-gtk
  webkit2gtk-4.1

  libhandy
  libosinfo
  libvirt-glib

  libportal
  libportal-gtk3
)

makedepends=(
  git
  vala
  meson
  itstool
  gobject-introspection

  mold
  clang
)

pkgver() {
  cd ${pkgname}

  local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
  local _commits=$(git rev-list --count HEAD)
  local _commith=$(git rev-parse --short HEAD)

  echo ${_version}+${_commits}+${_commith}
}

build() {
  arch-meson ${pkgname} build

  meson compile -C build
}

package() {
  meson install -C build --destdir ${pkgdir}
}
