pkgname=libadwaita

arch=(x86_64)
pkgver=1.7.alpha+4495+daa4586f
pkgrel=12

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/gnome/libadwaita.git)

depends=(
	gtk4
	appstream
)

makedepends=(
	git
	meson
	sassc
)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D introspection=disabled #TODO: verify
		-D vapi=false #TODO: verify

		-D tests=false
		-D examples=false
	)

	arch-meson $pkgname build ${options[@]}

	meson compile -C build
}

package() {
	meson install -C build --destdir ${pkgdir}
}
