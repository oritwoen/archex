pkgname=gnome-bluetooth

arch=(x86_64)
pkgver=47.1+3862+7cd8c7a2
pkgrel=1

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/gnome/gnome-bluetooth.git)

provides=(gnome-bluetooth-3.0)
replaces=(gnome-bluetooth-3.0)
conflicts=(gnome-bluetooth-3.0)

depends=(
  gtk4
  gsound
  upower
  libnotify
  libadwaita
)

makedepends=(
  git
  meson
  glib2-devel
  gobject-introspection

  mold
  clang
)

pkgver() {
  cd ${pkgname}

  local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
  local _commits=$(git rev-list --count HEAD)
  local _commith=$(git rev-parse --short HEAD)

  echo ${_version}+${_commits}+${_commith}
}

build() {
  arch-meson ${pkgname} build

  meson compile -C build
}

package() {
  meson install -C build --destdir ${pkgdir}
}
