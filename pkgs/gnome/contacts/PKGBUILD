pkgname=gnome-contacts

arch=(x86_64)
pkgver=49.alpha+4160+ea3b3394
pkgrel=1

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/gnome/gnome-contacts.git)

depends=(
  gtk4
  folks
  qrencode
  libadwaita
  libportal-gtk4
)

makedepends=(
  git
  vala
  meson

  mold
  clang
)

pkgver() {
  cd ${pkgname}

  local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
  local _commits=$(git rev-list --count HEAD)
  local _commith=$(git rev-parse --short HEAD)

  echo ${_version}+${_commits}+${_commith}
}

build() {
  local options=(
    -D manpage=false
  )

  arch-meson ${pkgname} build ${options[@]}

  meson compile -C build
}

package() {
  meson install -C build --destdir ${pkgdir}
}
