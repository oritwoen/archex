pkgname=desktop

arch=(x86_64)
pkgrel=18
pkgver=44.1+6651+3bd4bdde

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/gnome/gnome-desktop.git)

provides=(
	gnome-desktop
	gnome-desktop-4
	gnome-desktop-common
	libgnome-desktop-3.so 
	libgnome-{bg,desktop,rr}-4.so
)

replaces=(
	gnome-desktop
	gnome-desktop-4
	gnome-desktop-common
)

conflicts=(
	gnome-desktop
	gnome-desktop-4
	gnome-desktop-common
)

depends=(
	gtk3
	gtk4
	systemd
	gdk-pixbuf2
)

makedepends=(
	git
	meson
	gobject-introspection
)

pkgver() {
	cd gnome-desktop

	local _version=$(grep -m 2 version meson.build | awk -F"'" 'NR==2{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D desktop_docs=false
		-D debug_tools=false
		-D introspection=true #TODO: verify if false
		-D legacy_library=true #TODO: verify if false
	)

	arch-meson gnome-desktop build ${options[@]}

	meson compile -C build
}

package() {
	meson install -C build --destdir ${pkgdir}
}
