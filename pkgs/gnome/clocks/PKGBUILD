pkgname=clocks

arch=(x86_64)
pkgver=47.0+2836+3c92b2e
pkgrel=17

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/gnome/gnome-clocks.git)

replaces=(gnome-clocks)

depends=(
	gtk4
	geoclue
	gnome-desktop-4

	libadwaita
	libgweather-4
)

makedepends=(
	git
	vala
	meson
	itstool
)

pkgver() {
	cd gnome-clocks

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	arch-meson gnome-clocks build

	meson compile -C build
}

package() {
	meson install -C build --destdir ${pkgdir}
}
