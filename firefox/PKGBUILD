# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=firefox
pkgver=112.0a1+656177+34c5d377283c
pkgrel=1
pkgdesc="Developer Edition of the popular Firefox web browser"
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
url="https://www.mozilla.org/firefox/channel/#developer"
depends=('dbus-glib'
         'ffmpeg'
         'gtk3'
         'libpulse'
         'libxt'
         'mime-types'
         'nss'
         'ttf-font')
makedepends=('cbindgen'
             'clang'
             'diffutils'
             'dump_syms'
             'imake'
             'inetutils'
             'jack'
             'lld'
             'llvm'
             'mesa'
             'nasm'
             'nodejs'
             'python'
             'rust'
             'unzip'
             'wasi-compiler-rt'
             'wasi-libc'
             'wasi-libc++'
             'wasi-libc++abi'
             'xorg-server-xvfb'
             'yasm'
             'zip')
optdepends=('networkmanager: Location detection via available WiFi networks'
            'libnotify: Notification integration'
            'pulseaudio: Audio support'
            'speech-dispatcher: Text-to-Speech'
            'hunspell-en_US: Spell checking, American English'
            'xdg-desktop-portal: Screensharing with Wayland')
replaces=('firefox-developer')
options=(!emptydirs !makeflags !strip !lto !debug)
source=(hg+https://hg.mozilla.org/mozilla-central mozconfig firefox.desktop)
sha512sums=('SKIP' 'SKIP' 'SKIP')

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Arch Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact foutrelis@archlinux.org for
# more information.
_google_api_key=AIzaSyDwr302FpOSkGRpLlUpPThNTDPbXcIn_FM

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Arch Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact heftig@archlinux.org for
# more information.
_mozilla_api_key=e05d56db0a694edc8b5aaebda3f2db6a

pkgver() {
    cd mozilla-central

    local _version=$(cat browser/config/version.txt)

    echo ${_version}+$(hg id --num --rev tip)+$(hg id -i)
}

prepare() {
  mkdir -p mozbuild
  mkdir -p mozobjdir

  echo -n "$_google_api_key" > google-api-key
  echo -n "$_mozilla_api_key" > mozilla-api-key
}

build() {
  cd mozilla-central

  export MOZ_NOSPAM=1
  export MOZCONFIG="$srcdir/mozconfig"
  export MOZ_OBJDIR="$srcdir/mozobjdir"
  export MOZBUILD_STATE_PATH="$srcdir/mozbuild"
  export MACH_BUILD_PYTHON_NATIVE_PACKAGE_SOURCE=pip

  echo "Add keyfiles..."
  cat >.mozconfig ../mozconfig - <<END
ac_add_options --with-google-location-service-api-keyfile=${srcdir}/google-api-key
ac_add_options --with-google-safebrowsing-api-keyfile=${srcdir}/google-api-key
ac_add_options --with-mozilla-api-keyfile=${srcdir}/mozilla-api-key
END

  ./mach build
}

package() {
  cd mozilla-central
  
  DESTDIR="$pkgdir" ./mach install

  install -Dvm644 ../$pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
}
