# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jan@archlinux.org>

pkgname=gnome-desktop
pkgdesc="Library with common API for various GNOME modules"
license=(
	GPL 
	LGPL
)

arch=(x86_64)
pkgrel=1
pkgver=44.0+6638+8fb7ee85

url=https://gitlab.gnome.org/GNOME/gnome-desktop

makedepends=(
	gtk3
	gtk4
	systemd
	iso-codes
	libseccomp
	bubblewrap
	libxkbcommon
	xkeyboard-config
	gobject-introspection
	gsettings-desktop-schemas
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/gnome-desktop.git#branch=master)

provides=(
	gnome-desktop-4
	gnome-desktop-common
	libgnome-desktop-3.so 
	libgnome-{bg,desktop,rr}-4.so
)

replaces=(
	gnome-desktop-4
	gnome-desktop-common
)

conflicts=(
	gnome-desktop-4
	gnome-desktop-common
)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 2 version meson.build | awk -F"'" 'NR==2{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D desktop_docs=false
		-D debug_tools=false
	)

	arch-meson $pkgname build "${options[@]}"

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
