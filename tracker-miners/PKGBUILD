# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=tracker-miners
pkgdesc="Collection of data extractors for Tracker/Nepomuk"
license=(GPL)

arch=(x86_64)
pkgver=3.6.2+14371+823f3fc02
pkgrel=4

url="https://wiki.gnome.org/Projects/Tracker"

depends=(
  exempi
  giflib
  gst-plugins-base-libs
  libcue
  libexif
  libgexiv2
  libgrss
  libgsf
  libgxps
  libiptcdata
  libnm
  libosinfo
  libseccomp
  poppler-glib
  totem-plparser
  tracker3
  upower
)

makedepends=(
  asciidoc
  git
  meson
)

b2sums=(SKIP)
groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/tracker-miners.git#branch=master)

provides=(tracker3-miners)
replaces=(tracker3-miners)
conflicts=(tracker3-miners)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    local meson_options=(
        -D functional_tests=false
        -D man=false
    )
    
    arch-meson $pkgname build "${meson_options[@]}"
    
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
