# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=nautilus
pkgdesc="Default file manager for GNOME"
license=(GPL)

arch=(x86_64)
pkgrel=2
pkgver=46.alpha.0+24455+e253a639d

url="https://wiki.gnome.org/Apps/Files"

depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  gnome-autoar
  gnome-desktop-4
  gstreamer
  gst-plugins-base-libs
  gtk4
  gvfs
  hicolor-icon-theme
  libadwaita
  libcloudproviders
  libgexiv2
  libportal
  libportal-gtk4
  pango
  tracker3
)

makedepends=(
  appstream-glib
  gi-docgen
  git
  gobject-introspection
  meson
  tracker3-miners
)

b2sums=(SKIP)
groups=(gnome-apps)
source=(git+https://gitlab.gnome.org/GNOME/nautilus.git#branch=main)

provides=(libnautilus-extension)
replaces=(libnautilus-extension)
conflicts=(libnautilus-extension)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 2 version meson.build | awk -F"'" 'NR==2 {print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D docs=false
    -D extensions=true
    -D introspection=true
    -D packagekit=true
    -D selinux=false
    -D cloudproviders=true
    -D tests=none
	)
	
	arch-meson $pkgname build "${meson_options[@]}"
	
	meson compile -C build
}

package () {
    meson install -C build --destdir "$pkgdir"
}
