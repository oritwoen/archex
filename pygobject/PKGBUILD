# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=pygobject
pkgdesc="Python bindings for GLib/GObject/GIO/GTK"
license=(LGPL)

arch=(x86_64)
pkgver=+4275+a858105c
pkgrel=1

url="https://wiki.gnome.org/Projects/PyGObject"

depends=(
	python
	gobject-introspection-runtime
)

makedepends=(
	python-cairo
	python-sphinx
	gobject-introspection
	python-sphinx_rtd_theme
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/pygobject.git#branch=master)

provides=(python-gobject)
replaces=(python-gobject)
conflicts=(python-gobject)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $6}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)
	
	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D tests=false
	)
	
	arch-meson $pkgname build "${options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
	
	python -m compileall -d /usr/lib "$pkgdir/usr/lib"
	python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}
