# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-contacts
pkgdesc="Contacts Manager for GNOME"
license=(GPL2)

arch=(x86_64)
pkgver=45.beta+3906+b00e56e9
pkgrel=3

url="https://wiki.gnome.org/Apps/Contacts"

depends=(
  evolution-data-server
  folks
  gnome-online-accounts
  gtk4
  libadwaita
  libgee
  libportal-gtk4
  qrencode
)

makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  vala
)

b2sums=(SKIP)
groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/gnome-contacts.git#branch=main)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    #export CC=gcc
    #export CXX=gcc++

    CFLAGS+=" -Wno-error=int-conversion -Wno-error=return-type"

	arch-meson $pkgname build \
		-D manpage=false

	ninja -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
