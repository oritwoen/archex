# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-contacts
pkgdesc="Contacts Manager for GNOME"
license=(GPL2)

arch=(x86_64)
pkgver=45.0+3936+2a3f51d3
pkgrel=8

url="https://wiki.gnome.org/Apps/Contacts"

depends=(
  cairo
  dconf
  evolution-data-server
  folks
  gdk-pixbuf2
  glib2
  gnome-online-accounts
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  libgoa
  libportal
  libportal-gtk4
  pango
  qrencode
)

makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  vala
)

b2sums=(SKIP)
groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/gnome-contacts.git#branch=main)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    local meson_options=(
        -D profile=default
        -D camera=true
        -D manpage=false
        -D docs=false
        -D goa=true
    )
    
    arch-meson $pkgname build "${meson_options[@]}"

    meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
