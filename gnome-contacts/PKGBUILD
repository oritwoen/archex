# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-contacts
pkgdesc="Contacts Manager for GNOME"
license=(GPL2)

arch=(x86_64)
pkgver=46.alpha+3978+a19dac1f
pkgrel=1

url=https://apps.gnome.org/Contacts

depends=(
	gtk4
	glib2
	pango
	cairo
	dconf
	folks
	libgee
	qrencode
	libportal
	gdk-pixbuf2
	libportal-gtk4
	hicolor-icon-theme
	evolution-data-server

	gnome-online-accounts
)

makedepends=(
	vala
	appstream-glib
	gobject-introspection
)

b2sums=(SKIP)
groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/gnome-contacts.git#branch=main)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	CFLAGS+=" -Wno-error=return-type"
	CFLAGS+=" -Wno-error=incompatible-function-pointer-types"

	local options=(
		-D manpage=false
	)

	arch-meson $pkgname build "${options[@]}"

	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
