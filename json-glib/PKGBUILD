# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=json-glib
pkgver=1.8.1+1319+9434759
pkgrel=8
pkgdesc="JSON library built on GLib"
url="https://wiki.gnome.org/Projects/JsonGlib"
arch=(x86_64)
license=(GPL)

depends=(glib2)
makedepends=(gobject-introspection git meson gi-docgen docbook-xsl)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/json-glib.git#branch=master)

depends=(libg{lib,object,io}-2.0.so)
provides=(libjson-glib-1.0.so)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D introspection=enabled
		-D gtk_doc=disabled
		-D man=false
		-D tests=false
		-D nls=enabled
	)

	arch-meson $pkgname build "${meson_options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
