# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=mesa
pkgdesc="An open-source implementation of the OpenGL specification"
pkgver=23.1.0+166489+75ccf7c003c
pkgrel=1
arch=('x86_64')
makedepends=('python-mako' 'libxml2' 'libx11' 'xorgproto' 'libdrm' 'libxshmfence' 'libxxf86vm'
             'libxdamage' 'libvdpau' 'libva' 'wayland' 'wayland-protocols' 'zstd'
             'elfutils' 'llvm' 'libomxil-bellagio' 'libclc' 'clang' 'libglvnd' 'libunwind' 'lm_sensors'
             'libxrandr' 'glslang' 'meson')
url="https://www.mesa3d.org/"
license=('custom')
source=('git+https://gitlab.freedesktop.org/mesa/mesa.git#branch=main')
sha512sums=('SKIP')

depends=('libglvnd' 'expat' 'libdrm' 'libelf' 'libclc' 'clang' 'zstd' 'wayland' 'libx11' 'libxshmfence' 'libdrm' 'llvm-libs' 'lm_sensors'  'libomxil-bellagio' 'libunwind'  'libxxf86vm' 'libxdamage' 'libxshmfence')
optdepends=('opencl-headers: headers necessary for OpenCL development' 'vulkan-mesa-layer: a vulkan layer to display information using an overlay'
'opengl-man-pages: for the OpenGL API man pages'
            'mesa-vdpau: for accelerated video playback'
            'libva-mesa-driver: for accelerated video playback')

conflicts=('vulkan-mesa-layer' 'opencl-mesa' 'vulkan-radeon' 'libva-mesa-driver' 'mesa-vdpau' 'mesa-libgl')
replaces=('vulkan-mesa-layer' 'opencl-mesa' 'vulkan-radeon' 'libva-mesa-driver' 'mesa-vdpau' 'mesa-libgl')
provides=('vulkan-mesa-layer' 'opencl-mesa' 'vulkan-radeon' 'libva-mesa-driver' 'mesa-vdpau' 'vulkan-driver' 'mesa-libgl' 'opengl-driver')

pkgver () {
    cd ${pkgname}

    local _version=$(cat VERSION | cut -d "-" -f1)

    echo ${_version}+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
}

build() {
    #export CC=gcc
    #export CXX=g++

    arch-meson $pkgname build \
        -D platforms=x11,wayland \
        -D egl-native-platform=wayland \
        -D android-libbacktrace=disabled \
        -D dri3=enabled \
        -D gallium-drivers=radeonsi,svga,virgl,zink \
        -D gallium-extra-hud=true \
        -D gallium-vdpau=enabled \
        -D gallium-omx=bellagio \
        -D gallium-va=enabled \
        -D gallium-xa=enabled \
        -D gallium-opencl=icd \
        -D gallium-rusticl=true \
        -D opencl-spirv=true \
        -D vulkan-drivers=amd,virtio-experimental \
        -D shader-cache=enabled \
        -D vulkan-layers=device-select,overlay \
        -D shared-glapi=enabled \
        -D gles1=disabled \
        -D gles2=enabled \
        -D gbm=enabled \
        -D glx=dri \
        -D egl=enabled \
        -D glvnd=true \
        -D microsoft-clc=disabled \
        -D llvm=enabled \
        -D shared-llvm=enabled \
        -D valgrind=disabled \
        -D libunwind=enabled \
        -D lmsensors=enabled \
        -D enable-glcpp-tests=false \
        -D osmesa=false \
        -D tools=[] \
        -D power8=disabled \
        -D xlib-lease=enabled \
        -D zstd=enabled \
        -D vmware-mks-stats=true \
        -D vulkan-beta=true \
        -D video-codecs=vc1dec,h264dec,h264enc,h265dec,h265enc \
        -D gallium-d3d12-video=enabled 

                

    meson configure --no-pager build
    
    ninja -C build
}

package() {
    DESTDIR="${pkgdir}" ninja $NINJAFLAGS -C build install

    # remove script file from /usr/bin
    # https://gitlab.freedesktop.org/mesa/mesa/issues/2230
    rm "${pkgdir}/usr/bin/mesa-overlay-control.py"
    rmdir "${pkgdir}/usr/bin"

    # indirect rendering
    ln -s /usr/lib/libGLX_mesa.so.0 "${pkgdir}/usr/lib/libGLX_indirect.so.0"

    #install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" "${srcdir}/LICENSE"
}
