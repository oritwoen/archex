# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-logs
pkgdesc="A log viewer for the systemd journal"
license=(GPL)

arch=(x86_64)
pkgver=45.0+1355+8b7d44d
pkgrel=1

url="https://wiki.gnome.org/Apps/Logs"

depends=(
	gtk4
	dconf
	glib2
	pango
	systemd
	libadwaita
	hicolor-icon-theme
	gsettings-desktop-schemas
)

makedepends=(
	appstream-glib
)

b2sums=(SKIP)
groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/gnome-logs.git#branch=master)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $6}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)
	
	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D man=false
	)
	
	arch-meson $pkgname build "${options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
