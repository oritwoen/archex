# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Bruno Pagani <archange@archlinux.org>

pkgname=gnome-firmware
pkgdesc="Manage firmware on devices supported by fwupd"
license=(GPL2)

arch=(x86_64)
pkgver=45.1+357+d322e38
pkgrel=1

url=https://gitlab.gnome.org/world/gnome-firmware

depends=(
	gtk4
	glib2
	fwupd
	systemd
	libxmlb
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/World/gnome-firmware.git#branch=master)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D consolekit=false
		-D man=false
	)

	arch-meson $pkgname build "${options[@]}"

	meson compile -C build
}

package() {
	meson install -C build --destdir "${pkgdir}"
}
