# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=sushi
pkgdesc="A quick previewer for Nautilus"
license=(GPL2)

arch=(x86_64)
pkgver=46.alpha+941+cd171b4
pkgrel=11

url="https://gitlab.gnome.org/GNOME/sushi"

depends=(
  evince
  gjs
  gst-plugin-gtk
  gst-plugins-base-libs
  gtk3
  gtksourceview4
  libsoup3
  webkit2gtk-4.1
)

makedepends=(
  git
  gobject-introspection
  meson
)

b2sums=(SKIP)
groups=(gnome)
source=(git+https://gitlab.gnome.org/GNOME/sushi.git#branch=master)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
	local meson_options=(
		-D wayland=enabled
    -D X11=disabled
    -D profile=default
	)
	
	arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
