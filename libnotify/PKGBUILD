# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libnotify
pkgdesc="Library for sending desktop notifications"
license=(LGPL)

arch=(x86_64)
pkgver=0.8.3+504+150af91
pkgrel=1

url=https://gitlab.gnome.org/GNOME/libnotify

license=(LGPL)

depends=(
	gdk-pixbuf2
)

makedepends=(
	gtk3
	xmlto
	gobject-introspection
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/libnotify.git#branch=master)

provides=(libnotify.so)

pkgver() {
	cd $pkgname

	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D tests=false
		-D man=false
		-D gtk_doc=false
		-D docbook_docs=disabled
	)

	arch-meson $pkgname build "${options[@]}"

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
