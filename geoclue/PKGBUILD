# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=geoclue
pkgdesc="Modular geoinformation service built on the D-Bus messaging system"
license=(LGPL)

arch=(x86_64)
pkgver=2.7.1+1048+a9caa55
pkgrel=1

url=https://gitlab.freedesktop.org/geoclue/geoclue/-/wikis/home

depends=(
	avahi
	libsoup3
	json-glib
	libmm-glib
)

makedepends=(
	vala
	systemd
	libnotify
	gobject-introspection
)

b2sums=(SKIP)
source=(git+https://gitlab.freedesktop.org/geoclue/geoclue.git#branch=master)

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Arch Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact heftig@archlinux.org for
# more information.
_mozilla_api_key=e05d56db0a694edc8b5aaebda3f2db6a

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $6}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D gtk-doc=false
		-D dbus-srv-user=geoclue
		-D mozilla-api-key="$_mozilla_api_key"
	)
	
	arch-meson geoclue build "${options[@]}"
	
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"

	echo 'u geoclue - "Geoinformation service" /var/lib/geoclue' |
		install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/geoclue.conf"

	echo 'd /var/lib/geoclue 0755 geoclue geoclue' |
		install -Dm644 /dev/stdin "$pkgdir/usr/lib/tmpfiles.d/geoclue.conf"
}
