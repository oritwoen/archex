# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>

pkgname=planify
pkgdesc="Task manager with Todoist support"
license=('GPL-3.0-or-later')

arch=(x86_64)
pkgver=4.4.1+1465+38976941
pkgrel=1

url="https://github.com/alainm23/planify"

depends=(
	gxml
	granite7
	libadwaita
	webkitgtk-6.0
	libportal-gtk4
	gtksourceview5
	evolution-data-server
)

makedepends=(
	vala
	gobject-introspection
)

b2sums=(SKIP)
source=(git+https://github.com/alainm23/planify.git#branch=master)

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	CFLAGS+=" -Wno-error=incompatible-function-pointer-types"

	arch-meson ${pkgname} build
	
	meson compile -C build
}

package() {
	local app_id=io.github.alainm23.planify

	meson install -C build --destdir "$pkgdir"

	ln -s "/usr/bin/${app_id}" "$pkgdir/usr/bin/$pkgname"
}
