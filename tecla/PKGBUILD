# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>

pkgname=tecla
pkgdesc="Tecla is a keyboard layout viewer"
license=(GPL3)

arch=(x86_64)
pkgver=46.0+149+73fad8f
pkgrel=1

url="https://gitlab.gnome.org/GNOME/tecla"

depends=(
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  libxkbcommon
  pango
  wayland
)

makedepends=(
  git
  meson
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/tecla.git)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $2}')
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build
    
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
