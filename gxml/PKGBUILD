# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Fernando Fernández <fernando at softwareperonista dot com dot ar>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>
# Contributor: Balló György
# Contributor: Artem Vorotnikov <artem at vorotnikov dot me>

#TODO: check is still needed

pkgname=gxml
pkgdesc="GObject-based XML parser and writer library"
license=('LGPL')

arch=('x86_64')
pkgver=0.20.5+2139+54a9251
pkgrel=1

url=https://wiki.gnome.org/GXml

depends=(
	glib2
	libgee
	libxml2
)
	
makedepends=(
	vala
	gobject-introspection
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/gxml.git#branch=master)

pkgver() {
	cd $pkgname
	
	local _version=$(grep -m 1 version meson.build | awk -F"'" '{print $8}')
	local _commits=$(git rev-list --count HEAD)
	local _commith=$(git rev-parse --short HEAD)

	echo ${_version}+${_commits}+${_commith}
}

build() {
	local options=(
		-D docs=false
		-D experimental=true
	)

	arch-meson $pkgname build "${options[@]}"

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
