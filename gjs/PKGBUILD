# Maintainer: Dominik Opyd (oritwoen) <dominik.opyd@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgdesc="Javascript Bindings for GNOME"
license=(GPL)

arch=(x86_64)
pkgver=1.77.90+5320+61024387
pkgrel=4

url="https://wiki.gnome.org/Projects/Gjs"

depends=(
  cairo
  dconf
  gobject-introspection-runtime
  js115
  libsysprof-capture
  readline
)

makedepends=(
  dbus
  git
  gobject-introspection
  meson
)

b2sums=(SKIP)
source=(git+https://gitlab.gnome.org/GNOME/gjs.git#branch=master)
provides=(libgjs.so)

groups=(gnome-core)

pkgver() {
    cd $pkgname
    
    local _version=$(grep -m 1 "version:" meson.build | cut -d "'" -f 8)
    local _commits=$(git rev-list --count HEAD)
    local _commith=$(git rev-parse --short HEAD)

    echo ${_version}+${_commits}+${_commith}
}

build() {
    arch-meson $pkgname build \
      -D profiler=disabled \
      -D installed_tests=false \
      -D skip_dbus_tests=true \
      -D skip_gtk_tests=true

    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
